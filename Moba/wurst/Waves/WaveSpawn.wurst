package WaveSpawn

import ClosureTimers
import UnitTypeConfig
import PathUnit
import LinkedList
import Farm
import ErrorHandling
import General
import SoundUtils
import TeamUtil

constant SOUND_FIRST_SPAWN = new SoundDefinition(Sounds.clanInvitation, false, false)

constant real SPAWN_INTERVAL = 50
boolean spawnStarted = false
boolean spawnStopped = false

public Spawner array barracksSpawner
public Spawner array workshopSpawner

// spawn position is inverted, because units naturally return to their spawn position
vec2 array spawnPosition = [gg_rct_BrownCastle.getCenter(), gg_rct_GreenCastle.getCenter()]
angle array spawnAngle = [angle(45), angle(225)]

@inline public function spawnUnit( int team, int unitId, vec2 startPos) returns unit
    let spawnedUnit = createUnit(players[Teams.army(team)], unitId, spawnPosition[team] , spawnAngle[team])
    spawnedUnit.setPos(startPos)
    return spawnedUnit

public function stopSpawning()
    spawnStopped = true

class Spawner
    LinkedList<int> unitTypes = new LinkedList<int>()
    AssignedPath path
    vec2 spawnPos
    bool enabled = true
    int team

    construct(int team, AssignedPath path, vec2 spawnPos)
        this.team = team
        this.path = path
        this.spawnPos = spawnPos

    function enable()
        this.enabled = true

    function disable()
        this.enabled = false

    function addUnitType(vararg int uTypes)
        for uType in uTypes
            this.unitTypes.add(uType)

    function spawn()
        var counter = 0
        for int unitId in this.unitTypes
            doAfter(counter*0.25, () -> (begin
                if this.enabled
                    let spawnedUnit = spawnUnit(this.team, unitId, this.spawnPos)
                    new PathUnitNew(spawnedUnit, this.path)

            end))
            counter++

    function spawn(int uType)
        if this.enabled
            let spawnedUnit = spawnUnit(this.team, uType, this.spawnPos)
            new PathUnitNew(spawnedUnit, this.path)

public function startSpawning()
    if spawnStarted
        error("Spawning has already started.")
    else
        if DEBUG_MODE
            spawnWave()

        doAfter(SPAWN_INTERVAL) ->
            SOUND_FIRST_SPAWN.play()
            print("|cffffcc00The battle begins!|r")

            if GetLocalPlayer().getId() <= Teams.last1()
                PingMinimap(udg_GreenBarracks[0].getX(), udg_GreenBarracks[0].getY(), 5)
                PingMinimap(udg_GreenBarracks[1].getX(), udg_GreenBarracks[1].getY(), 5)
                PingMinimap(udg_GreenBarracks[2].getX(), udg_GreenBarracks[2].getY(), 5)
            else
                PingMinimap(udg_BrownBarracks[0].getX(), udg_BrownBarracks[0].getY(), 5)
                PingMinimap(udg_BrownBarracks[1].getX(), udg_BrownBarracks[1].getY(), 5)
                PingMinimap(udg_BrownBarracks[2].getX(), udg_BrownBarracks[2].getY(), 5)

        doPeriodically(SPAWN_INTERVAL, (CallbackPeriodic cb) -> (begin
            if spawnStopped
                destroy cb
            else
                spawnWave()
        end))
        spawnStarted = true


function setupSpawnUnits()
    barracksSpawner[0] = new Spawner(TEAM_1, AssignedPath.GREEN_TOP, gg_rct_GreenSpawnTop.getCenter())
    barracksSpawner[1] = new Spawner(TEAM_1, AssignedPath.GREEN_MID, gg_rct_GreenSpawnMid.getCenter())
    barracksSpawner[2] = new Spawner(TEAM_1, AssignedPath.GREEN_BOT, gg_rct_GreenSpawnBot.getCenter())

    barracksSpawner[3] = new Spawner(TEAM_2, AssignedPath.BROWN_TOP, gg_rct_BrownSpawnTop.getCenter())
    barracksSpawner[4] = new Spawner(TEAM_2, AssignedPath.BROWN_MID, gg_rct_BrownSpawnMid.getCenter())
    barracksSpawner[5] = new Spawner(TEAM_2, AssignedPath.BROWN_BOT, gg_rct_BrownSpawnBot.getCenter())

    workshopSpawner[0] = new Spawner(TEAM_1, AssignedPath.GREEN_TOP, gg_rct_GreenWorkShopTop.getCenter())
    workshopSpawner[1] = new Spawner(TEAM_1, AssignedPath.GREEN_BOT, gg_rct_GreenWorkShopBot.getCenter())

    workshopSpawner[2] = new Spawner(TEAM_2, AssignedPath.BROWN_TOP, gg_rct_BrownWorkShopTop.getCenter())
    workshopSpawner[3] = new Spawner(TEAM_2, AssignedPath.BROWN_BOT, gg_rct_BrownWorkShopBot.getCenter())

    for i = 0 to 3
        if i <= 1
            workshopSpawner[i].addUnitType(MORTAR_TEAM)
        else
            workshopSpawner[i].addUnitType(MORTAR_TEAM_2)

    for i = 0 to 5
        if i != 1 and i != 4
            if i <= 2
                barracksSpawner[i].addUnitType(FOOTMAN, FOOTMAN, FOOTMAN, RIFLEMAN, RIFLEMAN)
            else
                barracksSpawner[i].addUnitType(FOOTMAN_2, FOOTMAN_2, FOOTMAN_2, RIFLEMAN_2, RIFLEMAN_2)
        else
            if i <= 2
                barracksSpawner[i].addUnitType(FOOTMAN, FOOTMAN, FOOTMAN, KNIGHT, RIFLEMAN, RIFLEMAN)
            else
                barracksSpawner[i].addUnitType(FOOTMAN_2, FOOTMAN_2, FOOTMAN_2, KNIGHT_2, RIFLEMAN_2, RIFLEMAN_2)


function spawnWave()
    if spawnStopped
        return

    income()

    for i=0 to 5
        barracksSpawner[i].spawn()

    doAfter(6, () -> (begin
        for i=0 to 3
            workshopSpawner[i].spawn()
    end))

    
init
    setupSpawnUnits()
