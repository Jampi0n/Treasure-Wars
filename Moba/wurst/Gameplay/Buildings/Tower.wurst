package Tower

import BuildingTypes

import LinkedList
import HashMap
import DamageDetection
import HeroAI
import TeamUtil

class TowerPos
    real x
    real y
    Tower tower

    construct(real x, real y, Tower tower)
        this.x = x
        this.y = y
        this.tower = tower

    function inRange(unit whichUnit, real range) returns boolean
        let deltaX = whichUnit.getX() - x
        let deltaY = whichUnit.getY() - y
        let dist = deltaX*deltaX + deltaY*deltaY
        return dist < range * range


public class Tower

    private unit tower
    private unit currentTarget = null
    private boolean heroFocus = false
    private TowerPos pos

    private static constant towerList = new LinkedList<Tower>
    private static constant towerGroup = CreateGroup()
    private static constant towerMap = new HashMap<unit,Tower>
    private static constant towerPos1 = new LinkedList<TowerPos>
    private static constant towerPos2 = new LinkedList<TowerPos>

    static function classInit()

        CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ATTACKED)
        ..addAction() ->
            if towerGroup.contains(GetAttacker())
                towerMap.get(GetAttacker()).onAttack(GetTriggerUnit())

        CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction() ->
            if towerGroup.contains(GetTriggerUnit())
                destroy towerMap.get(GetTriggerUnit())

        addOnDamageFunc(Condition(function onDamage))

        let g = CreateGroup()
        unit u

        GroupEnumUnitsOfPlayer(g, players[Teams.army1()], null)

        while true
            u = FirstOfGroup(g)
            if u == null
                break
            g.removeUnit(u)
            let id = u.getTypeId()
            
            if id == BASE_TOWER or id == GATE_TOWER or id == OUTER_TOWER
                new Tower(u)

        GroupEnumUnitsOfPlayer(g, players[Teams.army2()], null)

        while true
            u = FirstOfGroup(g)
            if u == null
                break
            g.removeUnit(u)
            let id = u.getTypeId()
            
            if id == BASE_TOWER or id == GATE_TOWER or id == OUTER_TOWER
                new Tower(u)

        g.destr()

    construct(unit tower)
        this.tower = tower
        towerList.add(this)
        towerGroup.addUnit(tower)
        towerMap.put(tower, this)

        this.pos = new TowerPos(tower.getX(), tower.getY(), this)

        if tower.getOwner().getId() == Teams.army1()
            towerPos1.add(this.pos)
        else
            towerPos2.add(this.pos)

    ondestroy
        towerGroup.removeUnit(this.tower)
        towerList.remove(this)
        towerMap.remove(this.tower)
        if tower.getOwner().getId() == Teams.army1()
            towerPos1.remove(this.pos)
        else
            towerPos2.remove(this.pos)

    static function onDamage()
        let attacker = GetEventDamageSource()
        let target = GetTriggerUnit()

        if target.isType(UNIT_TYPE_HERO) and attacker.isType(UNIT_TYPE_HERO) and GetEventDamage() > 0.1
            if IsUnitAlly(attacker, players[Teams.army1()]) and IsUnitAlly(target, players[Teams.army2()])
                for TowerPos pos in towerPos2
                    if pos.inRange(attacker, 800)
                        pos.tower.attackHero(attacker)
            else if IsUnitAlly(attacker, players[Teams.army2()]) and IsUnitAlly(target, players[Teams.army1()])
                for TowerPos pos in towerPos1
                    if pos.inRange(attacker, 800)
                        pos.tower.attackHero(attacker)

    function determineTarget()
        this.heroFocus = false
        currentTarget = tower.findTarget(800, AbilityTargetFocus.ENEMY_UNIT, AbilityTargetPriority.LOW_DISTANCE, null)

    function onAttack(unit attacked)
        //tower is not focusing its target
        if attacked != currentTarget
            //target cannot attack its target
            if not tower.issueTargetOrder("attack", currentTarget)
                //choose new target and attack it
                determineTarget()
                tower.issueTargetOrder("attack", currentTarget)

                if currentTarget == null
                    currentTarget = attacked

                if currentTarget.isType(UNIT_TYPE_HERO)
                    let ai = getAI(GetTriggerUnit())
                    if ai != null
                        ai.attackedByTower()

    //a hero inside the tower range damages an allied hero
    function attackHero(unit targetHero)
        if not this.heroFocus
            this.currentTarget = targetHero
            this.heroFocus = true

init
    Tower.classInit()
    