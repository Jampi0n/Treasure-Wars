package BuildingTypes

import StandardTextTags
import PlayerControl
import ClosureTimers
import initlater WaveSpawn
import initlater UnitTypeConfig
import General
import BuffTypeConfig
import UnitWrapper
import ErrorHandling
import HeroControl
import Buff
import ObjectEditingPreset
import SoundUtils
import Assets
import LinkedList
import OnAttack
import TeamUtil

public constant int ALTAR = 'h002'
public constant int ARCANE_SANCTUM = 'h008'
public constant int ARCANE_VAULT = 'h005'
public constant int BARRACKS = 'h003'
public constant int BARRACKS_REBUILD = 'h01E'
public constant int BASE_TOWER = 'h009'
public constant int BLACKSMITH = 'h004'
public constant int CASTLE = 'h001'
public constant int FARM = 'h007'
public constant int OUTER_TOWER = 'h016'
public constant int GATE_TOWER = 'h006'
public constant int HERO_TOWER = 'n000'
public constant int WORKSHOP = 'h00A'
public constant int WORKSHOP_REBUILD = 'h01A'

public unit array barracks = [udg_GreenBarracks[0], udg_GreenBarracks[1], udg_GreenBarracks[2], udg_BrownBarracks[0], udg_BrownBarracks[1], udg_BrownBarracks[2]]
public boolean array barracksAlive = [true, true, true, true, true, true]
public unit array workshop = [udg_GreenWorkShop[0], udg_GreenWorkShop[1], udg_BrownWorkShop[0], udg_BrownWorkShop[1]]
public boolean array workshopAlive = [true, true, true, true]
public unit array blacksmith = [udg_GreenBlacksmith[0], udg_GreenBlacksmith[1], udg_BrownBlacksmith[0], udg_BrownBlacksmith[1]]
public boolean array blacksmithAlive = [true, true, true, true]

public int array UPGRADES = ['R000', 'R001', 'R002', 'R003']
public int array UPGRADE_ITEMS = ['I002', 'I003', 'I004', 'I005']

int array numberOfFarms = [26, 26]

constant real BARRACKS_REBUILD_TIME = -1 //300 // -1 means the building cannot be rebuilt
constant real WORKSHOP_REBUILD_TIME = -1 //60 // -1 means the building cannot be rebuilt

constant int SHOP_SHARING = 'Aall'

constant int TOWER_FOCUS_MAX_STACKS = 5
constant int TOWER_FOCUS_ARMOR = 20
constant real TOWER_FOCUS_DUR = 5
constant int TOWER_FOCUS_ABILITY_ID = 'A00O'

BuffType buffTowerFocus

constant int TOWER_FOCUS_BUFF_ID = compiletime(createAuraBuff("Tower Focus", "This unit was hit by a Tower; it has reduced armor.", Icons.bTNHumanWatchTower))
constant int TOWER_FOCUS_ABILTIY_ID = compiletime(createNegativeBuffAbility(1, TOWER_FOCUS_BUFF_ID, "", "chest"))

constant SOUND_UPGRADE = new SoundDefinition(Sounds.blacksmithWhat1, false, true)
constant SOUND_REPAIR_1 = new SoundDefinition(Sounds.peonRepair1, false, true)
constant SOUND_REPAIR_2 = new SoundDefinition(Sounds.peonRepair2, false, true)
constant SOUND_REPAIR_3 = new SoundDefinition(Sounds.peonRepair3, false, true)
constant SOUND_SIEGE_ENGINE = new SoundDefinition(Sounds.steamTankWhat3, false, true)
constant SOUND_MORTAR_TEAM = new SoundDefinition(Sounds.mortarTeamReady1, false, true)
constant SOUND_TOME = new SoundDefinition(Sounds.tomes, false, true)
constant SOUND_CALL_TO_ARMS = new SoundDefinition(Sounds.humanCallToArmsWhat1, false, true)

let a = "|cffffcc00gdg|r"

enum VictoryState
	UNDECIDED
	WIN_TEAM1
	WIN_TEAM2

VictoryState victoryState


int birth = 0
vec2 array militaSpawn = [gg_rct_GreenCastle.getCenter(), gg_rct_BrownCastle.getCenter()]
LinkedList<unit> array militia = [new LinkedList<unit>(), new LinkedList<unit>()]
boolean array removeMilita = [true, true]
rect array militiaDespawn = [gg_rct_GreenMilitiaDespawn, gg_rct_BrownMilitiaDespawn]


function setVictoryState(VictoryState newState)
	victoryState = newState

	if newState != VictoryState.UNDECIDED
		gameOver = true

		//stops heroes from reviving
		for i = 0 to MAX_HERO_COUNT-1
			for Hero hero in Players.heroList(i)
				hero.destroyTimer()


		let g = CreateGroup()
		for i = 0 to bj_MAX_PLAYER_SLOTS-1
			GroupEnumUnitsOfPlayer(g, players[i], null)
			unit u
			while true
				u = FirstOfGroup(g)
				if u == null
					break
				g.removeUnit(u)
				u.pause()

		g.destr()
				 
		int winTeam
		string teamName
		if newState == VictoryState.WIN_TEAM1
			teamName = Teams.COLORED_NAME[TEAM_1]
			winTeam = TEAM_1
		else
			teamName = Teams.COLORED_NAME[TEAM_2]
			winTeam = TEAM_2


		printTimed("Team "+teamName+" has destroyed the enemy castle and wins the game!", 60)

		//ScoreTable.extendToStatTable()
		stopSpawning()
		StatTable.create()

		printTimed("You can now view player stats on the Stats-Multiboard.", 60)

		if false
			doAfter(5) ->
				let winList = Teams.HEROES[winTeam]
				let defeatList = Teams.HEROES[Teams.invert(winTeam)]

				for int id in winList
					CustomVictoryBJ( players[id] , false, true )

				for int id in defeatList
					CustomDefeatBJ( players[id], "Your team's castle has been destroyed." )


		


function onBuildingDeath()
	let building = GetTriggerUnit()
	if building.isType(UNIT_TYPE_STRUCTURE)

		let unitId = building.getTypeId()
		let buildingOwnerId = building.getOwner().getId()
		let killer = GetKillingUnit().getOwner()
		let killerId = killer.getId()

		int bounty = 0
		int globalBounty = 0

		if unitId == FARM
			bounty = 50
			globalBounty = 50
			numberOfFarms[Players.team(buildingOwnerId)]--

		else if unitId == OUTER_TOWER
			bounty = 75
			globalBounty = 75

		else if unitId == GATE_TOWER
			bounty = 100
			globalBounty = 100

		else if unitId == BASE_TOWER
			bounty = 100
			globalBounty = 100

		else if unitId == BARRACKS
			bounty = 250
			globalBounty = 250

			if BARRACKS_REBUILD_TIME != -1
				let newBuilding = createUnit(building.getOwner(), BARRACKS_REBUILD, building.getPos(), (270).fromDeg())
				..setAnimation("birth")
				..setTimeScale(60/BARRACKS_REBUILD_TIME)
				..setHP(0.5)
				UnitAddType(newBuilding, UNIT_TYPE_ANCIENT)
				

				for i=0 to 5
					
					let count = (BARRACKS_REBUILD_TIME / 0.1 +0.5).toInt()

					if(building==barracks[i])
						barracks[i] = newBuilding
						barracksAlive[i] = false
						barracksSpawner[i].disable()
						doPeriodicallyCounted(0.1, count, (CallbackCounted cb) -> (begin
							if cb.isLast()
								let owner = newBuilding.getOwner()
								let pos = newBuilding.getPos()
								newBuilding.remove()
								barracks[i] = createUnit(owner, BARRACKS, pos, (270).fromDeg())
								barracksAlive[i] = true
								barracksSpawner[i].enable()
							else
								newBuilding.setHP(max(0.5, (1-cb.getCount()/count) * newBuilding.getMaxHP() ) )
						end))
			else
				for i=0 to 5
					if(building==barracks[i])
						barracks[i] = null
						barracksAlive[i] = false
						barracksSpawner[i].disable()


		else if unitId == WORKSHOP
			bounty = 150
			globalBounty = 150

			if WORKSHOP_REBUILD_TIME != -1
				let newBuilding = createUnit(building.getOwner(), WORKSHOP_REBUILD, building.getPos(), (270).fromDeg())
				..setAnimation("birth")
				..setTimeScale(0)
				..setHP(0.5)
				UnitAddType(newBuilding, UNIT_TYPE_ANCIENT)

				for i=0 to 3
					if(building==workshop[i])
						workshop[i] = newBuilding
						workshopAlive[i] = false
						workshopSpawner[i].disable()
			else
				for i=0 to 3
					if(building==workshop[i])
						workshop[i] = null
						workshopAlive[i] = false
						workshopSpawner[i].disable()
					
		else if unitId == ARCANE_VAULT
			bounty = 100
			globalBounty = 100

		else if unitId == BLACKSMITH
			bounty = 150
			globalBounty = 150

			for i=0 to 3
				if building==blacksmith[i]
					blacksmithAlive[i] = false


		else if unitId == CASTLE

			if victoryState == VictoryState.UNDECIDED
				if building.getOwner() == players[Teams.army1()]
					setVictoryState(VictoryState.WIN_TEAM2)
				else if building.getOwner() == players[Teams.army2()]
					setVictoryState(VictoryState.WIN_TEAM1)
				else
					error("Castle killed for player "+building.getOwner().getId().toString())
			
		if bounty + globalBounty > 0
			let team = Players.team(buildingOwnerId)

			if Players.team(killerId) != team
				PlayerWrapper.addGoldEx(killerId, bounty + globalBounty)
				PlayerWrapper.statBuildingsRazed[killerId]++
				createGoldBountyTextTag(building, bounty + globalBounty, killer)

			
			for int id in Players.heroList(team)
				let p = players[id]
				if p != killer
					PlayerWrapper.addGoldEx(id, globalBounty)
					createGoldBountyTextTag(building, globalBounty, p)



function onBuildingAbility()
	let itemTypeId = GetSoldItem().getTypeId()
	let building = GetTriggerUnit()
	let hero = GetBuyingUnit()
	let heroPos = hero.getPos3Zero()
	let team = Players.team(building.getOwner().getId())
	bool buildingAbilityUsed = true
	if itemTypeId == 'I000' //Rebuild Workshop
		doPeriodicallyCounted(1, 5, (CallbackCounted cb) -> begin
			let r = GetRandomInt(1, 3)
			if r == 1
				SOUND_REPAIR_1.playOnPoint(heroPos).snd.setVolume(350)
			else if r == 2
				SOUND_REPAIR_2.playOnPoint(heroPos).snd.setVolume(350)
			else
				SOUND_REPAIR_3.playOnPoint(heroPos).snd.setVolume(350)
		end)
		
		building.removeAbility(SHOP_SHARING)

		let count = (WORKSHOP_REBUILD_TIME / 0.1 +0.5).toInt()

		building.setTimeScale(60/WORKSHOP_REBUILD_TIME)

		doPeriodicallyCounted(0.1, count, (CallbackCounted cb) -> (begin
			if cb.isLast()
				UnitRemoveType(building, UNIT_TYPE_ANCIENT)
				for i = 0 to 3
					if building == workshop[i]
						workshopAlive[i] = true
						let owner = building.getOwner()
						let pos = building.getPos()
						building.remove()
						workshop[i] = createUnit(owner, WORKSHOP, pos, (270).fromDeg())
						break
			else
				building.setHP(max(0.5, (1-cb.getCount()/count) * building.getMaxHP() ) )
		end))
	else if itemTypeId == 'I001' //Siege Engine
		SOUND_SIEGE_ENGINE.playOnPoint(heroPos).snd.setVolume(150)
		for i = 0 to 3
			if building == workshop[i]
				workshopSpawner[i].spawn(TANK)
				break

	else if itemTypeId == 'I007' //Mortar Team
		SOUND_MORTAR_TEAM.playOnPoint(heroPos).snd.setVolume(150)
		for i = 0 to 3
			if building == workshop[i]
				workshopSpawner[i].spawn(team == TEAM_1 ? MORTAR_TEAM : MORTAR_TEAM_2)
				break
		

	else if itemTypeId == 'I002' //Melee Attack
		SOUND_UPGRADE.playOnPoint(heroPos).snd.setVolume(150)
		let current = GetPlayerTechCount(building.getOwner(), 'R000', true)
		if current == 4
			SetPlayerTechMaxAllowed(building.getOwner(), itemTypeId, 0)
		SetPlayerTechResearched(building.getOwner(), 'R000', current + 1)

		let g = CreateGroup()
		GroupEnumUnitsOfPlayer(g, building.getOwner(), null)
		unit u
		while(true)
			u = FirstOfGroup(g)
			if u == null
				break
			g.removeUnit(u)
			if not u.isType(UNIT_TYPE_STRUCTURE)
				u.wrapper().updateBaseStatsUnits()
		g.destr()

	else if itemTypeId == 'I003' //Ranged Attack
		SOUND_UPGRADE.playOnPoint(heroPos).snd.setVolume(150)
		let current = GetPlayerTechCount(building.getOwner(), 'R001', true)
		if current == 4
			SetPlayerTechMaxAllowed(building.getOwner(), itemTypeId, 0)
		SetPlayerTechResearched(building.getOwner(), 'R001', current + 1)

		let g = CreateGroup()
		GroupEnumUnitsOfPlayer(g, building.getOwner(), null)
		unit u
		while(true)
			u = FirstOfGroup(g)
			if u == null
				break
			g.removeUnit(u)
			if not u.isType(UNIT_TYPE_STRUCTURE)
				u.wrapper().updateBaseStatsUnits()
		g.destr()

	else if itemTypeId == 'I004' //Heavy Armor
		SOUND_UPGRADE.playOnPoint(heroPos).snd.setVolume(150)
		let current = GetPlayerTechCount(building.getOwner(), 'R002', true)
		if current == 4
			SetPlayerTechMaxAllowed(building.getOwner(), itemTypeId, 0)
		SetPlayerTechResearched(building.getOwner(), 'R002', current + 1)

		let g = CreateGroup()
		GroupEnumUnitsOfPlayer(g, building.getOwner(), null)
		unit u
		while(true)
			u = FirstOfGroup(g)
			if u == null
				break
			g.removeUnit(u)
			if not u.isType(UNIT_TYPE_STRUCTURE)
				u.wrapper().updateBaseStatsUnits()
		g.destr()

	else if itemTypeId == 'I005' //Light Armor
		SOUND_UPGRADE.playOnPoint(heroPos).snd.setVolume(150)
		let current = GetPlayerTechCount(building.getOwner(), 'R003', true)
		if current == 4
			SetPlayerTechMaxAllowed(building.getOwner(), itemTypeId, 0)
		SetPlayerTechResearched(building.getOwner(), 'R003', current + 1)

		let g = CreateGroup()
		GroupEnumUnitsOfPlayer(g, building.getOwner(), null)
		unit u
		while(true)
			u = FirstOfGroup(g)
			if u == null
				break
			g.removeUnit(u)
			if not u.isType(UNIT_TYPE_STRUCTURE)
				u.wrapper().updateBaseStatsUnits()
		g.destr()

	else if itemTypeId == 'I006' //Call to Arms
		SOUND_CALL_TO_ARMS.playOnPoint(heroPos).snd.setVolume(350)
		removeMilita[team] = false
		for i = 0 to 15
			let spawnedUnit = spawnUnit(TEAM_1, MILITIA_DEFEND, militaSpawn[team])
			militia[team].add(spawnedUnit)
		doAfter(45, () -> (begin
			removeMilita[team] = true
			for u in militia[team]
				u.addBuff(u, -1, DISARMED, 1)
				u.issuePointOrder("move", militaSpawn[team])
				if militiaDespawn[team].contains(u.getPos())
					militia[team].remove(u)
					u.remove()
		end))
		doAfter(50, () -> (begin
			removeMilita[team] = true
			for u in militia[team]
				u.addBuff(u, -1, DISARMED, 1)
				u.issuePointOrder("move", militaSpawn[team])
				if militiaDespawn[team].contains(u.getPos())
					militia[team].remove(u)
					u.remove()
		end))
		doAfter(89, () -> (begin
			for u in militia[team]
				militia[team].remove(u)
				u.remove()
		end))



	else if itemTypeId == 'I008' //Teleport Top
		vec2 targetPos
		if Players.team(building.getOwner().getId()) == TEAM_1
			targetPos = gg_rct_GreenPortTop.getCenter()
		else
			targetPos = gg_rct_BrownPortTop.getCenter()

		new ChannelTeleport(hero, targetPos, 3, 200)

	else if itemTypeId == 'I009' //Teleport Middle
		vec2 targetPos
		if Players.team(building.getOwner().getId()) == TEAM_1
			targetPos = gg_rct_GreenPortMid.getCenter()
		else
			targetPos = gg_rct_BrownPortMid.getCenter()

		new ChannelTeleport(hero, targetPos, 3, 200)

	else if itemTypeId == 'I00A' //Teleport Bottom
		vec2 targetPos
		if Players.team(building.getOwner().getId()) == TEAM_1
			targetPos = gg_rct_GreenPortBot.getCenter()
		else
			targetPos = gg_rct_BrownPortBot.getCenter()

		new ChannelTeleport(hero, targetPos, 3, 200)

	//stats
	
	else  if itemTypeId == 'I00B' //Strength
		hero.wrapper().addStatAbsoluteBonus(Stat.STR, 1)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00C' //Willpower
		hero.wrapper().addStatAbsoluteBonus(Stat.AGI, 1)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00D' //Intelligenace
		hero.wrapper().addStatAbsoluteBonus(Stat.INT, 1)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00E' //Armor
		hero.wrapper().addStatAbsoluteBonus(Stat.AR, 1)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00F' //Attack Speed
		hero.wrapper().addStatAbsoluteBonus(Stat.AS, 1)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00G' //Hit Points
		hero.wrapper().addStatAbsoluteBonus(Stat.HP, 15)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00H' //Mana Point
		hero.wrapper().addStatAbsoluteBonus(Stat.MP, 10)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00I' //Hit Point Regeneration
		hero.wrapper().addStatAbsoluteBonus(Stat.HR, 3)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else if itemTypeId == 'I00J' //Mana Point Regeneration
		hero.wrapper().addStatAbsoluteBonus(Stat.MR, 1)
		SOUND_TOME.playOnPoint(heroPos).snd.setVolume(150)
		hero.addEffect(Abilities.aIimTarget, "chest").destr()
	else
		buildingAbilityUsed = false

	if buildingAbilityUsed
		standardTextTag(hero.getPos(), GetObjectName(itemTypeId)).setColor(255, 220, 0, 255)
		GetSoldItem().remove()



function onMilitiaDespawn(int team)
	if removeMilita[team]
		for u in militia[team]
			if Players.team(u.getOwner().getId()) == team and militiaDespawn[team].contains(u.getPos())
				militia[team].remove(u)
				u.remove()

constant HOME_BUFF_ID = compiletime(createAuraBuff("Protected", "This unit is protected; it is invulnerable and is being healed.", Icons.bTNInvulnerable))
constant HOME_ABILTIY_ID = compiletime(createPositiveBuffAbility(1, HOME_BUFF_ID, "Abilities\\Spells\\Human\\DivineShield\\DivineShieldTarget.mdl", ""))
constant BUFF_HOME = new BuffType(true, DispelLevel.NONE)

function homeInvulnerability()
	group nearby = CreateGroup()
	nearby.enumUnitsInRange(gg_rct_GreenAltar.getCenter(), 800)

	while(not nearby.isEmpty())
		let target = FirstOfGroup(nearby)
		nearby.removeUnit(target)
		if not target.isType(UNIT_TYPE_STRUCTURE) and IsUnitAlly(target, players[Teams.army1()]) 
			target.addBuff( target, 0.5, BUFF_HOME, 1)
			target.addBuff( target, 0.5, INVULERNABLE, 1)

	nearby.destr()

	nearby = CreateGroup()
	nearby.enumUnitsInRange(gg_rct_BrownAltar.getCenter(), 800)

	while(not nearby.isEmpty())
		let target = FirstOfGroup(nearby)
		nearby.removeUnit(target)
		if not target.isType(UNIT_TYPE_STRUCTURE) and IsUnitAlly(target, players[Teams.army2()]) 
			target.addBuff( target, 0.5, BUFF_HOME, 1)
			target.addBuff( target, 0.5, INVULERNABLE, 1)

	nearby.destr()

public function income()
	let goldFactor = 5

	for i = 0 to MAX_HERO_COUNT-1
		let farms = numberOfFarms[Players.team(i)]
		let gold = goldFactor * farms
		printTimedToPlayer("You get |cffffcc00"+gold.toString()+"|r gold (|cffffcc00+"+goldFactor.toString()+"|r for every Farm).", 8, players[i])
		PlayerWrapper.addGoldEx(i, gold)
	

init
	CreateTrigger()
	..addAction(function onBuildingDeath)
	..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)

	CreateTrigger()
	..addAction(function onBuildingAbility)
	..registerAnyUnitEvent(EVENT_PLAYER_UNIT_SELL_ITEM)


	CreateTrigger()
	..registerTimerEvent(0.1, true)
	..addAction()->
		onMilitiaDespawn(TEAM_1)
		onMilitiaDespawn(TEAM_2)

	BUFF_HOME.setupBuffWithIcon(HOME_BUFF_ID, HOME_ABILTIY_ID)

	CreateTrigger()
	..registerTimerEvent(0.2, true)
	..addAction(function homeInvulnerability)


	buffTowerFocus = new BuffType(false, DispelLevel.DISPEL)
	buffTowerFocus.setupBuffWithIcon(TOWER_FOCUS_BUFF_ID, TOWER_FOCUS_ABILTIY_ID)

	buffTowerFocus.addFirstFunction( (Buff whichBuff) -> (begin
		whichBuff.getTarget().wrapper()
		..addStatRelativeBonus(Stat.AR, -TOWER_FOCUS_ARMOR * 10)
		whichBuff.setData(1)
	end))

	buffTowerFocus.addStackFunction( (Buff whichBuff) -> (begin
		let stacks = whichBuff.getData() + 1
		if stacks <= TOWER_FOCUS_MAX_STACKS
			whichBuff.setData(stacks)
			whichBuff.getTarget().wrapper()
			..addStatRelativeBonus(Stat.AR, -TOWER_FOCUS_ARMOR * 10)
	end))


	buffTowerFocus.addEndFunction( (Buff whichBuff) -> (begin
		let stacks = whichBuff.getData()
		whichBuff.getTarget().wrapper()
		..addStatRelativeBonus(Stat.AR, TOWER_FOCUS_ARMOR * stacks * 10)
	end))

	OnAttack.addOnAttackFunc((unit attacker, unit target, boolean isActive) -> (begin
		if attacker.getAbilityLevel(TOWER_FOCUS_ABILITY_ID)>0 and target.isType(UNIT_TYPE_HERO)
			target.addBuffReduced(attacker, TOWER_FOCUS_DUR, buffTowerFocus, 1)
	end))
		

	/*group buildings = CreateGroup()
	GroupEnumUnitsOfPlayer(buildings, players[10], null)
	for unit u in buildings
		for int i=0 to 4
			UnitShareVision(u, players[i], true)
			
	GroupEnumUnitsOfPlayer(buildings, players[11], null)
	for unit u in buildings
		for int i=5 to 9
			UnitShareVision(u, players[i], true)

	buildings.destr()*/