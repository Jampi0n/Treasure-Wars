package ItemInShop

import ErrorHandling
import LinkedList
import HashMap
import HashList
import ObjectIdGenerator
import ItemObjEditing
import ObjectIds

public constant SHOP_NUM_CATEGORIES = 7

public constant MAX_ITEMS_IN_LIST = 30
constant ITEM_DEFINITIONS = new HashMap<int, ItemDefinition>

public function getIconPath(string icon) returns string
    string s = icon
    if icon.length() < 20 or not icon.startsWith("ReplaceableTextures")
        s = "ReplaceableTextures\\CommandButtons\\" + s
    if not icon.endsWith(".blp")
        s += ".blp"
    return s

public function getItemDefintion(int itemId) returns ItemDefinition
    if not compiletime
        error("getItemDefintion cannot be used at runtime")

    return ITEM_DEFINITIONS.get(itemId)

public function iconFromItem(int itemId) returns int
    return ("T"+itemId.toRawCode().substring(1)).fromRawCode()


//compiletime to create an item object + icon texture
public function newItem(string icon) returns int
    let id = ITEM_ID_GEN.next()
    if compiletime
        let fullIcon = getIconPath(icon)
        let iDef = new ItemDefinition(id, 'rde1')
            ..setInterfaceIcon(fullIcon)
            ..setAbilities("")
            ..setActivelyUsed(false)
            ..setCooldownGroup("")
            ..setDescription("")
            ..setDroppedWhenCarrierDies(false)
            ..setUseAutomaticallyWhenAcquired(false)
            ..setPerishable(false)
            ..setNumberofCharges(0)
            ..setIgnoreCooldown(false)
            ..setLumberCost(0)
        ITEM_DEFINITIONS.put(id, iDef)
    return id


public class ItemInShop

    private int itemId
    private int category
    private int iconId
    private string icon
    private int totalCost
    private int singleCost
    private LinkedList<ItemInShop> requirements
    private int recipeSize = 1
    private HashList<ItemInShop> buildsIntoDirectly
    private HashList<ItemInShop> buildsIntoTransitively
    private LinkedList<ItemInShop> buildsIntoCombined
    private static constant allItems = new LinkedList<ItemInShop>

    private string name
    private string description

    private static LinkedList<ItemInShop> array itemsInCategory
    private static ItemInShop array itemsInCategoryArray
    private static int array itemsPerCategory
    private static constant itemToIndex = new HashMap<ItemInShop,int>

    private static constant itemIdToItemInShop = new HashMap<int,ItemInShop>

    static function classInit()
        for i=0 to SHOP_NUM_CATEGORIES-1
            itemsInCategory[i] = new LinkedList<ItemInShop>()

    /*
        Retrieves the object to this item id. It never returns null.
    */
    static function retrieveItemFromId(int itemId) returns ItemInShop
        if itemIdToItemInShop.has(itemId)
            return itemIdToItemInShop.get(itemId)
        else
            return new ItemInShop(itemId)

    /*
        creates an empty object. The object itself can be initialized later,
        but it can already be used as reference. Calculations depending on other
        items are done after all items have been initialized in finalize().
    */
    construct(int itemId)
        this.itemId = itemId
        itemIdToItemInShop.put(itemId, this)
        this.requirements = new LinkedList<ItemInShop>
        this.buildsIntoDirectly = new HashList<ItemInShop>
        this.buildsIntoTransitively = new HashList<ItemInShop>
        this.buildsIntoCombined = new LinkedList<ItemInShop>
        allItems.add(this)

    function setupItem(int category, int iconId, int totalCost)
        this.category = category
        this.iconId = iconId
        this.totalCost = totalCost
        this.singleCost = -1
        itemsInCategory[this.category].add(this)

        let itm = createItem(itemId, vec2(0,0))
        this.icon = itm.getIconPath()
        itm.remove()

    function setupStrings(string name, string desc)
        this.name = name
        this.description = desc

    function addRequirement(ItemInShop requirement)
        this.requirements.add(requirement)

    function getItemId() returns int
        return this.itemId

    function getIconId() returns int
        return this.iconId

    function getIcon() returns string
        return this.icon
        
    function getTotalCost() returns int
        return this.totalCost

    function getSingleCost() returns int
        return this.singleCost

    function getRequirements() returns LinkedList<ItemInShop>
        return this.requirements

    function getRecipeSize() returns int
        return this.recipeSize

    function getBuildsInto() returns LinkedList<ItemInShop>
        return this.buildsIntoCombined

    function getName() returns string
        return this.name

    function getDescription() returns string
        return this.description

    function calculateGoldCost()
        int childrenCost = 0
        for ItemInShop child in this.requirements
            childrenCost += child.totalCost

        this.singleCost = this.totalCost - childrenCost


    /** After all items have been created the buildsInto lists are built.**/
    static function finalize()
        for ItemInShop itm in allItems
            itm.calculateGoldCost()

        for ItemInShop itm in allItems

            for ItemInShop child in itm.requirements
                addBuildsInto(child, itm, 0)

        for ItemInShop itm in allItems
            let sortingList = new LinkedList<ItemInShop>()
            for ItemInShop b in itm.buildsIntoDirectly
                sortingList.add(b)
               
            sortingList.sortWith((a,b) -> ( a.getTotalCost() - b.getTotalCost() ) )
            for ItemInShop b in sortingList
                itm.buildsIntoCombined.add(b)

            sortingList.clear()
            for ItemInShop b in itm.buildsIntoTransitively
                sortingList.add(b)

            sortingList.sortWith((a,b) -> ( a.getTotalCost() - b.getTotalCost() ) )
            for ItemInShop b in sortingList
                itm.buildsIntoCombined.add(b)

            destroy sortingList


        for i = 0 to SHOP_NUM_CATEGORIES-1
            itemsInCategory[i].sortWith((a,b) -> ( a.getTotalCost() - b.getTotalCost() ) )

        for i = 0 to SHOP_NUM_CATEGORIES-1
            itemsPerCategory[i] = itemsInCategory[i].size()
            int j = 0
            for ItemInShop itm in itemsInCategory[i]
                itemsInCategoryArray[i * MAX_ITEMS_IN_LIST + j] = itm
                itemToIndex.put(itm, i * MAX_ITEMS_IN_LIST + j)
                j++

    private static function addBuildsInto(ItemInShop child, ItemInShop parent, int depth)
        if depth == 0
            if not child.buildsIntoDirectly.has(parent)
                child.buildsIntoDirectly.add(parent)
                if child.buildsIntoTransitively.has(parent)
                    child.buildsIntoTransitively.remove(parent)
        else if depth == 1
            if not child.buildsIntoDirectly.has(parent) and not child.buildsIntoTransitively.has(parent)
                child.buildsIntoTransitively.add(parent)
        else
            error("Too many children for item. depth = " + depth.toString())

        parent.recipeSize++
        for ItemInShop childChild in child.requirements
            addBuildsInto(childChild, parent, depth+1)

    static function getArrayIndex(ItemInShop itm) returns int
        return itemToIndex.get(itm)

    static function getIndex(ItemInShop itm) returns int
        return itemToIndex.get(itm) mod MAX_ITEMS_IN_LIST

    static function getCategory(ItemInShop itm) returns int
        return itemToIndex.get(itm) div MAX_ITEMS_IN_LIST

    static function getItemFromList(int category, int index) returns ItemInShop
        if index < getItemCount(category)
            return itemsInCategoryArray[category * MAX_ITEMS_IN_LIST + index]
        else
            return null

    static function getItemCount(int category) returns int
        return itemsPerCategory[category]

    static function getItemList(int category) returns LinkedList<ItemInShop>
        return itemsInCategory[category]

    static function getItemFromId(int itemId) returns ItemInShop
        return itemIdToItemInShop.get(itemId)

init
    ItemInShop.classInit()