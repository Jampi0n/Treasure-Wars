package BuffTypeConfig

import LinkedList
import Buff
import ObjectIdGenerator
import AbilityObjEditing
import BuffObjEditing
import ObjectIds

constant ABILITY_STUNNED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_SILENCED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_CYCLONED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_ENSNARED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_ENTANGLED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_MISS = compiletime(ABIL_ID_GEN.next())
constant ABILITY_INVIS = compiletime(ABIL_ID_GEN.next())

constant ABILITY_DISARMED_EFFECT = compiletime(ABIL_ID_GEN.next())
constant ABILITY_DISARMED_BUFF = compiletime(ABIL_ID_GEN.next())

constant ABILITY_UNTARGETABLE = compiletime(ABIL_ID_GEN.next())

constant BUFF_SILENCED = compiletime(BUFF_ID_GEN.next())
constant BUFF_CYCLONED = compiletime(BUFF_ID_GEN.next())
constant BUFF_ENSNARED = compiletime(BUFF_ID_GEN.next())
constant BUFF_ENTANGLED = compiletime(BUFF_ID_GEN.next())
constant BUFF_MISS = compiletime(BUFF_ID_GEN.next())
constant BUFF_DISARMED = compiletime(BUFF_ID_GEN.next())
constant BUFF_UNTARGETABLE = compiletime(BUFF_ID_GEN.next())

public constant STUNNED = new BuffType(ABILITY_STUNNED, "creepthunderbolt", 'BPSE', false, true)
public constant SILENCED = new BuffType(ABILITY_SILENCED, "soulburn", BUFF_SILENCED, false, true)
public constant CYCLONED = new BuffType(ABILITY_CYCLONED, "cyclone", BUFF_CYCLONED, false, true)
public constant ENSNARED = new BuffType(ABILITY_ENSNARED, "ensnare", BUFF_ENSNARED, false, true)
public constant ENTANGLED = new BuffType(ABILITY_ENTANGLED, "entanglingroots", BUFF_ENTANGLED, false, true)
public constant MISS = new BuffType(ABILITY_MISS, "drunkenhaze", BUFF_MISS, false, true)
public constant INVIS = new BuffType(ABILITY_INVIS, "invisibility", 'Binv', true, true)

public constant DISARMED = new BuffType(false, true)

public constant UNTARGETABLE = new BuffType(true, true)

public function AbilityDefinition.setDummySkill()
    this
        ..setLevels(1)
        ..setCooldown(1, 0)
        ..setCastRange(1, 92083)
        ..setManaCost(1, 0)
        ..setMissileArt("")
        ..setMissileArc(0)
        ..setMissileSpeed(10000)
        ..setTargetsAllowed(1, "air,enemies,friend,ground,invulnerable,item,neutral,structure,vulnerable,ward")
        ..setRequirements("")
        ..setRequiredLevel(6)
        ..setHeroAbility(true)
        ..setDurationHero(1, 0)
        ..setDurationNormal(1, 0)

@compiletime
function createObjectData()
    new AbilityDefinitionThunderBoltCreep(ABILITY_STUNNED)
        ..setDummySkill()
    new AbilityDefinitionFirelordSoulBurn(ABILITY_SILENCED)
        ..setDummySkill()
        ..setBuffs(1, int2fourchar(BUFF_SILENCED))
        ..setDamageAmount(1, 0)
        ..setDamagePenalty(1, 0)
        ..setDamagePeriod(1, 300)
    new AbilityDefinitionCyclone(ABILITY_CYCLONED)
        ..setDummySkill()
        ..setBuffs(1, "Bcyc,"+int2fourchar(BUFF_CYCLONED))
    new AbilityDefinitionEnsnare(ABILITY_ENSNARED)
        ..setDummySkill()
        ..setBuffs(1, "Bena,"+int2fourchar(BUFF_ENSNARED))
    new AbilityDefinitionEntanglingRootscreep(ABILITY_ENTANGLED)
        ..setDummySkill()
        ..setDamageperSecond(1, 0)
        ..setBuffs(1, int2fourchar(BUFF_ENTANGLED))
    new AbilityDefinitionInvisibility(ABILITY_INVIS)
        ..setDummySkill()
        ..setArtTarget("")
    new AbilityDefinitionBrewmasterDrunkenHaze(ABILITY_MISS)
        ..setDummySkill()
        ..setChanceToMiss(1, 1)
        ..setMovementSpeedModifier(1, 0)
        ..setAreaofEffect(1, 0.)
        ..setBuffs(1, int2fourchar(BUFF_MISS))
    new AbilityDefinitionAuraSlow(ABILITY_DISARMED_BUFF)
        ..setAreaofEffect(1, 1)
        ..setMovementSpeedFactor(1, 0)
        ..setTargetsAllowed(1, "invulnerable,self,vulnerable")
    new AbilityDefinitionCargoHoldBurrow(ABILITY_DISARMED_EFFECT)
        ..setCargoCapacity(1, 0)
    new BuffDefinition(BUFF_CYCLONED, 'Bcy2')
    new BuffDefinition(BUFF_DISARMED, 'Basl')
        ..setIcon("ReplaceableTextures\\CommandButtons\\BTNHoldPosition.blp")
        ..setArtTarget(1, "")
        ..setTooltipNormal(1, "Disarmed")
        ..setName(1, "Disarmed")
        ..setTooltipNormalExtended(1, "This unit is disarmed; it cannot attack.")
    new BuffDefinition(BUFF_ENSNARED, 'Beng')
    new BuffDefinition(BUFF_ENTANGLED, 'BEer')
    new BuffDefinition(BUFF_MISS, 'Bcrs')
        ..setArtTarget(1, "")
        ..setName(1, "Miss")
        ..setTooltipNormal(1, "Miss")
        ..setTooltipNormalExtended(1, "This unit misses its attacks.")
    new BuffDefinition(BUFF_SILENCED, 'BNsi')
        ..setName(1, "Silence")
    new BuffDefinition(BUFF_UNTARGETABLE, 'Basl')
        ..setIcon("ReplaceableTextures\\CommandButtons\\BTNCancel.blp")
        ..setName(1, "Untargetable")
        ..setTooltipNormal(1, "Untargetable")
        ..setTooltipNormalExtended(1, "This unit is untargetable; it cannot be targeted.")

init
    var abilityList = new LinkedList<int>
    ..add(ABILITY_DISARMED_BUFF)
    ..add(ABILITY_DISARMED_EFFECT)
    DISARMED.setup(abilityList, BUFF_DISARMED)

    abilityList = new LinkedList<int>
    ..add(ABILITY_UNTARGETABLE)
    UNTARGETABLE.setup(abilityList, BUFF_UNTARGETABLE)
    UNTARGETABLE.addFirstFunction(()-> (begin
        let thisBuff = Buff.getTriggerBuff()
        UnitAddType(thisBuff.getTarget(), UNIT_TYPE_ANCIENT)
    end) )

    UNTARGETABLE.addEndFunction(()-> (begin
        let thisBuff = Buff.getTriggerBuff()
        UnitRemoveType(thisBuff.getTarget(), UNIT_TYPE_ANCIENT)
    end) )


