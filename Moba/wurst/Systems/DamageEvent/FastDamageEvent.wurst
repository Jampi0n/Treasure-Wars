package FastDamageEvent

import OnUnitEnterLeave
import ErrorHandling
import DamageDetection

public interface DamageFunction
    function damage(unit source, unit target, DamageEventUnit sourceWrapper, DamageEventUnit targetWrapper, real damage, bool attack, bool _active, bool advanced)

/** This buff is used to detect regular attacks **/
@configurable public var ON_HIT_BUFF = 0
/** This buff is used to detect special attacks **/
@configurable public var ON_HIT_BUFF_ARROW = 0
/** This buff is used to detect special attacks **/
@configurable public var ON_HIT_BUFF_ALTERNATE = 0

/*
Special attacks are mostly interesting for ranged units, because the hit can be delayed.
Using the arrow or alternate buffs, the unit will produce attacks with different buffs.
Even if the special attack phase is over, special attacks that hit afterwards can still be identified as special attacks.
*/

/**
Controls the size of fast data for every unit
Fast data is implemented as an array with inline access
Due to the array size limit, the number of units times the fast data size must be below 32678
Larger fast data sizes also require more time cleaning up when the unit is removed
**/
@configurable public var FAST_DATA_SIZE = 8

/**
The modDamage function modifies the damage. All damage modifiers are handled by this function to set up a deterministic order.
In order to improve speed, it is recommended to use DamageEventUnit save and load int functions whenever possible.
Instead of checking for an item the state can be saved when the item is picked up and dropped.
This is especially useful when a lot of damage modifiers are used, that only rarely are enabled.
**/
@configurable public function modDamage(unit _source, unit _target, DamageEventUnit _sourceWrapper, DamageEventUnit _targetWrapper, int _sourceTypeId, int _targetTypeId, real damage, bool _attack, bool _arrow, bool _alternate) returns real
    return damage

class DamageFunctionListElement
    DamageFunction f
    DamageFunctionListElement next
    DamageFunctionListElement prev
    DamageFunctionList list
    construct(DamageFunction f, DamageFunctionList list)
        this.f = f
        this.list = list

/**
Efficient list implementation for damage functions.
New elements are inserted at the head.
Elements are removed by pointer.
**/
class DamageFunctionList
    DamageFunctionListElement first = null
    function add(DamageFunction f) returns int
        let newElement = new DamageFunctionListElement(f, this)
        newElement.next = this.first
        newElement.prev = null
        if this.first != null
            this.first.prev = newElement
        this.first = newElement
        return newElement castTo int
    function remove(int pointer)
        let removeElement = pointer castTo DamageFunctionListElement
        if removeElement.list != this
            error("Tried to remove a damage function from a different list!")
        else
            if first == removeElement
                first = removeElement.next
                if removeElement.next != null
                    removeElement.next.prev = null
                    destroy removeElement
            else
                removeElement.prev.next = removeElement.next
                if removeElement.next != null
                    removeElement.next.prev = removeElement.prev
                destroy removeElement

    ondestroy
        DamageFunctionListElement current = this.first
        while current != null
            let next = current.next
            destroy current
            current = next

/**
A damage event group contains a damage function list.
Damage event groups are used to optimize common conditions and provide cleaner API.
A common condition is that neither the source nor target are buildings.
In order to avoid repeated conditions in every damage event function, the condition is put before the group is run.
**/
class DamageEventGroup
    int n = 0
    DamageFunctionList list = new DamageFunctionList()

    ondestroy
        destroy list

    function add(DamageFunction f) returns int
        n += 1
        return list.add(f)

    function remove(int pointer)
        n -= 1
        list.remove(pointer)

/*
Extension methods are used to prevent null checks and enable inlining.
*/
@inline function DamageEventGroup.hasEvents() returns bool
    return this.n > 0
@inline function DamageEventGroup.runEvents(unit source, unit target, DamageEventUnit sourceWrapper, DamageEventUnit targetWrapper, real damage, bool attack, bool arrow, bool alternate)
    DamageFunctionListElement current = this.list.first
    while current != null
        current.f.damage(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
        current = current.next

/*
DamageEventUnitType and DamageEventUnit provide two functions to retrieve these objects:
fromUnit/fromUnitTypeId: creates a new object if it does not exist already
get: returns a default object without events if it does not exist already
The get function is protected and is used to speed up the performance critical function onDamage.
There are no null checks necessary, because a default object without events will be used.
*/

/**
Damage events for unit types based on unit type id.
For permanent effects from certain unit types.
**/
public class DamageEventUnitType
    static function fromUnitTypeId(int unitTypeId) returns DamageEventUnitType
        var d = (DamageEvent.damageEventTable.loadInt(unitTypeId, 0) + 1) castTo DamageEventUnitType
        if d == DamageEvent.nullUnitType
            d = new DamageEventUnitType(unitTypeId)
        return d

    protected static function get(int unitTypeId) returns DamageEventUnitType
        return (DamageEvent.damageEventTable.loadInt(unitTypeId, 0) + 1) castTo DamageEventUnitType

    private int unitTypeId

    DamageEventGroup anyDamage = new DamageEventGroup()
    DamageEventGroup anyHit = new DamageEventGroup()
    DamageEventGroup attackDamageNoBuilding = new DamageEventGroup()
    DamageEventGroup attackHitNoBuilding = new DamageEventGroup()

    construct(int unitTypeId)
        this.unitTypeId = unitTypeId
        DamageEvent.damageEventTable.saveInt(unitTypeId, 0, this castTo int + 1)

/**
Damage events for units.
For temporary effects, e.g. based on items, spells, ...
**/
public class DamageEventUnit
    static function fromUnit(unit u) returns DamageEventUnit
        var d = (1 + DamageEvent.damageEventTable.loadInt(u.getHandleId(), 0)) castTo DamageEventUnit
        if d == DamageEvent.nullUnit
            d = new DamageEventUnit(u)
        return d

    protected static function get(unit u) returns DamageEventUnit
        return (1 + DamageEvent.damageEventTable.loadInt(u.getHandleId(), 0)) castTo DamageEventUnit
    protected static function classInit()
        onLeave() ->
            let d = DamageEventUnit.get(getEnterLeaveUnit())
            if d != null and d != DamageEvent.nullUnit
                destroy d

    protected static int array data

    private unit u
    private int h
    DamageEventGroup anyDamage = new DamageEventGroup()
    DamageEventGroup anyHit = new DamageEventGroup()
    DamageEventGroup attackDamageNoBuilding = new DamageEventGroup()
    DamageEventGroup attackHitNoBuilding = new DamageEventGroup()

    construct(unit u)
        this.u = u
        this.h = u.getHandleId()
        DamageEvent.damageEventTable.saveInt(h, 0, this castTo int - 1)
    ondestroy
        DamageEvent.damageEventTable.flushChild(h)
        for i = 0 to FAST_DATA_SIZE-1
            this.saveIntFast(i, 0)
        destroy anyDamage
        destroy anyHit
        destroy attackDamageNoBuilding
        destroy attackHitNoBuilding
    
    @inline
    function saveIntSlow(int key, int value)
        DamageEvent.damageEventTable.saveInt(h, key, value)
    @inline
    function saveIntFast(int key, int value)
        data[(this castTo int) * FAST_DATA_SIZE + key] = value

    @inline
    function loadIntSlow(int key) returns int
        return DamageEvent.damageEventTable.loadInt(h, key)
    @inline
    function loadIntFast(int key) returns int
        return data[(this castTo int) * FAST_DATA_SIZE + key]

public abstract class DamageEvent
    protected static hashtable damageEventTable = InitHashtable()
    protected static DamageEventUnit nullUnit = new DamageEventUnit(null)
    protected static DamageEventUnitType nullUnitType = new DamageEventUnitType(0)

    /*
    This function runs when a unit takes damage and should be optimized as best as possible.
    All function calls are inlined.
    */
    protected static function onDamage()
        let source = GetEventDamageSource()
        let target = GetTriggerUnit()
        real damage = GetEventDamage()
        bool attack = false
        bool alternate = false
        bool arrow = false
        if(target.hasAbility(ON_HIT_BUFF))
            target.removeAbility(ON_HIT_BUFF)
            attack = true
        if(target.hasAbility(ON_HIT_BUFF_ALTERNATE))
            target.removeAbility(ON_HIT_BUFF_ALTERNATE)
            attack = true
            alternate = true
        if(target.hasAbility(ON_HIT_BUFF_ARROW))
            target.removeAbility(ON_HIT_BUFF_ARROW)
            arrow = true
            attack = true

        let sourceTypeId = source.getTypeId()
        let sourceWrapper = DamageEventUnit.get(source)
        let sourceType = DamageEventUnitType.get(sourceTypeId)

        let targetTypeId = target.getTypeId()
        let targetWrapper = DamageEventUnit.get(target)
        let targetType = DamageEventUnitType.get(targetTypeId)
        
        damage = modDamage(source, target, sourceWrapper, targetWrapper, sourceTypeId, targetTypeId, damage, attack, arrow, alternate)
        sourceWrapper.anyDamage.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
        sourceType.anyDamage.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
        targetWrapper.anyHit.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
        targetType.anyHit.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
        if attack
            if not target.isType(UNIT_TYPE_STRUCTURE) and not source.isType(UNIT_TYPE_STRUCTURE)
                sourceWrapper.attackDamageNoBuilding.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
                sourceType.attackDamageNoBuilding.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
                targetWrapper.attackHitNoBuilding.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
                targetType.attackHitNoBuilding.runEvents(source, target, sourceWrapper, targetWrapper, damage, attack, arrow, alternate)
        damage = max(0, damage)
        BlzSetEventDamage(damage)
        
init
    if DamageEvent.nullUnit castTo int != 1
        error("Null unit is not mapped to index 1: " + (DamageEvent.nullUnit castTo int).toString())
    if DamageEvent.nullUnitType castTo int != 1
        error("Null unit type is not mapped to index 1: " + (DamageEvent.nullUnitType castTo int).toString())
    DamageEventUnit.classInit()
    addOnDamageFunc() -> 
        DamageEvent.onDamage()
