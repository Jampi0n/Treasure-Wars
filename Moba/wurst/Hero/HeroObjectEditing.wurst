package HeroObjectEditing

import AbilityObjEditing
import ObjectIds

/*
You cannot add learnable abilities to hero with code, it must be done with object data.
Hero skills will have specific abilityIds:

A+SpecialCharacter+Hero+Hotkey

SpecialCharacter: Z,Y,..
Anything that is not normally used in ability ids

Hero: Different for every hero: 0-9, a-z, A-Z
Means 62 heroes per special character.

PQWER


*/

public constant C_STR = color(255, 187, 102)
public constant C_INT = color(102, 187, 255)


public interface LevelString
    function getString(int level) returns string

public function real.str() returns string
    return this.toInt().toString()

public function int.str() returns string
        return this.toString()



public function AbilityDefinition.hideIcon()
        this
            ..setButtonPositionNormalX(0)
            ..setButtonPositionNormalY(-11)

public function AbilityDefinition.setLearnString(string intro, LevelString levelString)
    string learnString = intro+"|n"
    for i = 1 to 5
        learnString += getLevelString(i) + levelString.getString(i)

    this.setTooltipLearnExtended(learnString)
    

public function createNoEffectAbiltiy(int newId) returns AbilityDefinition
    let aDef = new AbilityDefinition(newId, 'Amgl')
        ..setCheckDependencies(false)
        ..setRequirements("")
    return aDef

public function createTargetMissile(int newId, int buffId) returns AbilityDefinitionAlchemistAcidBomb
    let aDef = new AbilityDefinitionAlchemistAcidBomb(newId)
    for i = 1 to 5
        aDef.setBuffs(i, int2fourchar(buffId))
        aDef.setArmorPenalty(i, 0)
        aDef.setPrimaryDamage(i, 0)
        aDef.setSecondaryDamage(i, 0)
        aDef.setDamageInterval(i, 10)
        aDef.setDurationHero(i, 1)
        aDef.setDurationNormal(i, 1)
        aDef.setAreaofEffect(i, 1)
    return aDef



public function getLevelString(int level) returns string
    return " |n|cffffcc00Level "+level.toString()+"|r - "

public function int.getBonusString() returns string
    if(this==0)
        return " "
    return " (+"+this.toString()+") "

public function real.getBonusString() returns string
    if(this==0)
        return " "
    return " (+"+this.toInt().toString()+") "

public function int.getBonusString(color c) returns string
    if(this==0)
        return " "
    return c.toColorString()+" (+"+this.toString()+")|r "

public function real.getBonusString(color c) returns string
    if(this==0)
        return " "
    return c.toColorString()+" (+"+this.toInt().toString()+")|r "

public function AbilityDefinition.setupWithHotKey(string name, string hotkey)
    this.presetHotkey(hotkey)
    this.setButtonPositionNormalY(2)
    this.setButtonPositionResearchY(0)
    this.setHeroAbility(true)
    this.setLevels(5)
    this.setRequiredLevel(1)
    this.setName(name)
    this.setTooltipLearn("("+hotkey+")"+" Learn "+name+" - [|cffffcc00Level %d|r]")
    for i = 1 to 5
        this.setTooltipNormal(i, "("+hotkey+") "+name+" - [|cffffcc00Level "+i.toString()+"|r]")
        
    if(hotkey=="Q")
        this..presetHotkey("Q")
            ..setButtonPositionNormalX(0)
            ..setButtonPositionResearchX(0)
        return

    if(hotkey=="W")
        this..presetHotkey("W")
            ..setButtonPositionNormalX(1)
            ..setButtonPositionResearchX(1)
        return

    if(hotkey=="E")
        this..presetHotkey("E")
            ..setButtonPositionNormalX(2)
            ..setButtonPositionResearchX(2)
        return

    if(hotkey=="R")
        this..presetHotkey("R")
            ..setButtonPositionNormalX(3)
            ..setButtonPositionResearchX(3)
            ..setRequiredLevel(6)
            ..setLevelSkipRequirement(3)
        return

    if(hotkey=="D")
        this..presetHotkey("D")
            ..setButtonPositionNormalX(2)
            ..setButtonPositionResearchX(2)
            ..setButtonPositionNormalY(1)
            ..setButtonPositionResearchY(1)
            ..setLevelSkipRequirement(4)
        return

    print("!")