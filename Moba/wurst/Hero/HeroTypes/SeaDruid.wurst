package SeaDruid

//3

import HeroType
import HeroControl
import Knockback3
import Missile
import TimerUtils
import ClosureTimers
import Fx
import SoundUtils

public constant HERO_SEA_DRUID = 'H00K'
HeroType heroType

constant ABILITY_ANCIENT_KNOWLEDGE = 'AZ3P'
constant ABILITY_TIDAL_FURY = 'AZ3Q'
constant ABILITY_GIANT_FISH = 'AZ3W'
constant ABILITY_MANA_SHIELD = 'AZ3E'
constant ABILITY_RITUAL_OF_THE_SEAS = 'AZ3R'


constant real array ANCIENT_KNOWLEDGE_HP_BONUS = [6, 5.5, 5, 4.5, 4]
constant real array ANCIENT_KNOWLEDGE_MP_BONUS = [12, 11, 10, 9, 8]

@compiletime
function ancientKnowledge()
    let aDef = createNoEffectAbiltiy(ABILITY_ANCIENT_KNOWLEDGE)
        ..setupIcon("PASSeaDruidism")
        ..setupWithHotKey("Ancient Knowledge", "D")
        ..setLearnString("Increases his maximum hit points for a portion of his maximum mana ponits and increases his maximum mana points by "+
        "a portion of his maximum hit points.", (int lvl) -> (begin
            return "1 hit point for every {0} mana points, 1 mana point for every {1} hit points".format(
            ANCIENT_KNOWLEDGE_HP_BONUS[lvl-1].toString(), ANCIENT_KNOWLEDGE_MP_BONUS[lvl-1].toString())
        end) )

    for lvl = 1 to 5
        aDef
            ..setTooltipNormalExtended(lvl, ("Increases maximum hit points by 1 for every {0} points of his maximum mana points and "+
            "increases his maximum mana points by 1 for every {1} points of his maximum hit points.").format(
                ANCIENT_KNOWLEDGE_HP_BONUS[lvl-1].toString(), ANCIENT_KNOWLEDGE_MP_BONUS[lvl-1].toString())
            )

constant real array TIDAL_FURY_HIT_DMG = [75, 125, 175, 225, 275]
constant real TIDAL_FURY_HIT_DMG_BONUS = 120

constant real array TIDAL_FURY_WAVE_DMG = [20, 35, 50, 65, 80]
constant real TIDAL_FURY_WAVE_DMG_BONUS = 60

@compiletime
function tidalFury()
    int array MANA = [100, 110, 120, 130, 140]
    real array COOLDOWN = [8, 8, 8, 8, 8]

    let aDef = new AbilityDefinitionPitLordHowlofTerror(ABILITY_TIDAL_FURY)
        ..setAnimationNames("attack,slam")
        ..setupIcon(Icons.bTNCrushingWave)
        ..setupWithHotKey("Tidal Fury", "Q")
        ..setArtCaster("")
        ..setLearnString("Knocks nearby enemy units back, dealing damage and releasing a crushing wave for every enemy unit hit. The wave deals damage to every enemy it passes through.", (int lvl) -> (begin
            return "{0} knockback damage, {1} wave damage.".format(
            TIDAL_FURY_HIT_DMG[lvl-1].str()+TIDAL_FURY_HIT_DMG_BONUS.getBonusString(C_STR),
            TIDAL_FURY_WAVE_DMG[lvl-1].str()+TIDAL_FURY_WAVE_DMG_BONUS.getBonusString(C_INT))
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setTargetsAllowed(lvl, "none")
            ..setTooltipNormalExtended(lvl, "Knocks nearby enemy units back, dealing {0} damage and releasing a crushing wave for every enemy unit hit. The wave deals {1} damage to every enemy it passes through.".format(
            TIDAL_FURY_HIT_DMG[lvl-1].str()+TIDAL_FURY_HIT_DMG_BONUS.getBonusString(C_STR),
            TIDAL_FURY_WAVE_DMG[lvl-1].str()+TIDAL_FURY_WAVE_DMG_BONUS.getBonusString(C_INT)))


constant real array FISH_DAMAGE = [100, 150, 200, 250, 300]
constant real FISH_DAMAGE_BONUS = 200

constant int array FISH_ATTACK_SPEED = [30, 40, 50, 60, 70]
constant int array FISH_MOVE_SPEED = [30, 40, 50, 60, 70]

constant real array FISH_SLOW_DURATION = [3, 3.5, 4, 4.5, 5]

constant real array FISH_AOE = [200, 225, 250, 275, 300]

constant int FISH_BUFF_ID = compiletime(createAuraBuff("Giant Fish", "This unit was hit by a giant fish; its movement and attack speed are slowed.", "BTNDeepseaMurloc"))
constant int FISH_ABILTIY_ID = compiletime(createNegativeBuffAbility(5, FISH_BUFF_ID, Abilities.stasisTotemTarget, "overhead"))

constant int FISH_IMPACT = compiletime(ArtilleryDetection.createDetectionDummy())
constant int FISH_ARTILLERY = compiletime(ArtilleryDetection.createArtilleryDummy(0, 0, 50, 3, "Fesh_Final2.mdx", 600, 0.3, FISH_IMPACT))

constant SOUND_FISH_IMPACT = new SoundDefinition(Sounds.artilleryCorpseExplodeDeath1, false, true)

BuffType buffFish


@compiletime
function giantFish()
    int array MANA = [115, 130, 145, 160, 175]
    real array COOLDOWN = [12, 12, 12, 12, 12]
    real array RANGE = [1200, 1200, 1200, 1200, 1200]

    let aDef = new AbilityDefinitionIllidanChannel(ABILITY_GIANT_FISH)
        ..setupIcon("BTNDeepseaMurloc")
        ..setupWithHotKey("Giant Fish", "W")
        ..setArtCaster("")
        ..setArtEffect("")
        ..setMissileArt("")
        ..setArtTarget("")
        ..setAnimationNames("spell")
        ..setupChannel(TargetType.AREA, "shockwave")
        ..setLearnString("Throws a giant fish at the target location, damaging and slowing enemies in the area.", (int lvl) -> (begin
            return "{0} damage, {1}% attack speed. {2}% movement speed, {3} seconds.".format(
                FISH_DAMAGE[lvl-1].str()+FISH_DAMAGE_BONUS.getBonusString(C_INT),
                FISH_ATTACK_SPEED[lvl-1].str(),
                FISH_MOVE_SPEED[lvl-1].str(),
                FISH_SLOW_DURATION[lvl-1].str()
            )
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setCastRange(lvl, RANGE[lvl-1])
            ..setAreaofEffect(lvl, FISH_AOE[lvl-1])
            ..setTooltipNormalExtended(lvl, "Throws a giant fish at the target location, dealing {0} magic damage and slowing attack speed by {1}% and movement speed by {2}% for {3} seconds.".format(
                FISH_DAMAGE[lvl-1].str()+FISH_DAMAGE_BONUS.getBonusString(C_INT),
                FISH_ATTACK_SPEED[lvl-1].str(),
                FISH_MOVE_SPEED[lvl-1].str(),
                FISH_SLOW_DURATION[lvl-1].str()
            ))


constant int MANA_SHIELD_BUFF_ID = compiletime(createBasicBuff("Mana Shield", "Uses mana to reduce incoming damage.", Icons.bTNNeutralManaShield, Abilities.manaShieldCaster, "origin"))

public constant real array MANA_SHIELD_REDUCTION = [0.2, 0.3, 0.4, 0.5, 0.6]
public constant real array MANA_PER_DAMAGE = [0.5, 0.45, 0.4, 0.35, 0.3]

public BuffType buffManaShield // the actual effect is in OnAttack

@compiletime
function manaShield()
    int array MANA = [50, 50, 50, 50, 50]
    real array MANA_PER_SECOND = [7, 8.5, 10, 11.5, 13]
    real array COOLDOWN = [5, 5, 5, 5, 5]


    let aDef = new AbilityDefinitionDemonHunterImmolation(ABILITY_MANA_SHIELD)
        ..setAnimationNames("")
        ..setupIcon(Icons.bTNNeutralManaShield)
        ..setIconTurnOff(Icons.bTNneutralManaShieldOff)
        ..setupWithHotKey("Mana Shield", "E")
        ..setLearnString("Reduces incoming damage at the cost of mana. Also drains mana while activated.", (int lvl) -> (begin
            return "{0}% reduction, {1} mana points per blocked damage point.".format(
                (MANA_SHIELD_REDUCTION[lvl-1]*100).str(), MANA_PER_DAMAGE[lvl-1].toString()
            )
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setDurationHero(lvl, 1)
            ..setDurationNormal(lvl, 1)
            ..setDamageperInterval(lvl, 0)
            ..setBuffs(lvl, int2fourchar(MANA_SHIELD_BUFF_ID)+",BEia")
            ..setManaDrainedperSecond(lvl, MANA_PER_SECOND[lvl-1])
            ..setTooltipTurnOff(lvl, "(E) Deactivate Mana Shield - [|cffffcc00Level "+lvl.toString()+"|r]")
            ..setTooltipTurnOffExtended(lvl, "Deactivate Mana Shield to stop draining mana.")
            ..setTargetsAllowed(lvl, "none")
            ..setTooltipNormalExtended(lvl, "Reduces incoming damage by {0}%, by spending {1} mana points for every blocked point of damage. Also drains mana while activated.".format(
                (MANA_SHIELD_REDUCTION[lvl-1]*100).str(), MANA_PER_DAMAGE[lvl-1].toString()
            ))



constant real array RITUAL_HP_DRAIN = [70, 100, 130, 160, 190]
constant real array RITUAL_MANA_DRAIN = [30, 45, 60, 75, 90]

constant real RITUAL_HP_DRAIN_BONUS = 120
constant real RITUAL_MANA_DRAIN_BONUS = 60

constant real array RITUAL_AOE = [900, 900, 900, 900, 900]

constant int array RITUAL_WAVE_COUNT = [6, 7, 8, 9, 10]

constant int RITUAL_HP_RATIO = 50
constant int RITUAL_MANA_RATIO = 25

@compiletime
function ritualOfTheSeas()
    int array MANA = [250, 300, 350, 400, 450]
    real array COOLDOWN = [90, 90, 90, 90, 90]


    let aDef = new AbilityDefinitionKeeperoftheGroveTranquility(ABILITY_RITUAL_OF_THE_SEAS)
        ..setAnimationNames("spell,channel")
        ..setupIcon("BTNGuardianofTheSea")
        ..setupWithHotKey("Ritual of the Seas", "R")
        ..setLearnString(CHANNELING_SPELL + "Sends out magical energy, that drains hit points and mana from nearby enemies.|nThe Sea Druid gets {0}% of the hit points and {1}% of the mana drained from enemy heroes.".format(
            RITUAL_HP_RATIO.str(),
            RITUAL_MANA_RATIO.str()
        )    
        , (int lvl) -> (begin
            return "{0} damage, {1} mana, lasts for {2} waves.".format(
                RITUAL_HP_DRAIN[lvl-1].str()+RITUAL_HP_DRAIN_BONUS.getBonusString(C_INT),
                RITUAL_MANA_DRAIN[lvl-1].str()+RITUAL_MANA_DRAIN_BONUS.getBonusString(C_INT),
                RITUAL_WAVE_COUNT[lvl-1].str()
            )
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setDurationHero(lvl, RITUAL_WAVE_COUNT[lvl-1]*2 + 0.1)
            ..setDurationNormal(lvl, RITUAL_WAVE_COUNT[lvl-1]*2 + 0.1)
            ..setEffects(lvl, "X000")

            ..setTargetsAllowed(lvl, "none")
            ..setTooltipNormalExtended(lvl, CHANNELING_SPELL + "Sends out magical energy, that drain {0} hit points and {1} mana from nearby enemies.|nLasts for {2} waves.|nThe Sea Druid gets {3}% of the hit points and {4}% of the mana drained from enemy heroes.".format(
                RITUAL_HP_DRAIN[lvl-1].str()+RITUAL_HP_DRAIN_BONUS.getBonusString(C_INT),
                RITUAL_MANA_DRAIN[lvl-1].str()+RITUAL_MANA_DRAIN_BONUS.getBonusString(C_INT),
                RITUAL_WAVE_COUNT[lvl-1].str(),
                RITUAL_HP_RATIO.str(),
                RITUAL_MANA_RATIO.str()
            ))

function setupStats()
    heroType
        //..setStat(Stat.AD, 10, 35)
        ..setStat(Stat.AR, 9, 24)
        ..setStat(Stat.AS, 0, 8)
        ..setStat(Stat.HP, 14, 61)
        ..setStat(Stat.HR, 14, 42)
        ..setStat(Stat.MP, 19, 84)
        ..setStat(Stat.MR, 18, 75)
        ..setStat(Stat.MS, 6, 27)
        ..setStat(Stat.STR, 20, 74)
        ..setStat(Stat.AGI, 12, 27)
        ..setStat(Stat.INT, 13, 47)

function onSpellCast()
    let spellId = GetSpellAbilityId()
    let caster = GetTriggerUnit()
    let level = caster.getAbilityLevel(spellId)

    if(spellId == ABILITY_TIDAL_FURY)
        let casterPos = caster.getPos()
        group nearby = CreateGroup()
        nearby.enumUnitsInRange(casterPos, 300)

        let hitDmg = TIDAL_FURY_HIT_DMG[level-1].bonus(caster.getStr(), TIDAL_FURY_HIT_DMG_BONUS)

        while(not nearby.isEmpty())
            unit target = FirstOfGroup(nearby)
            nearby.removeUnit(target)
            if not target.isType(UNIT_TYPE_STRUCTURE) and target.isTargetable(caster.getOwner(), false, true)
                caster.dealDamage(target, hitDmg, ReductionType.PHYSICAL)
                if target.isMovable()
                    Knockback3.add(target, caster.getPos().polarOffset(caster.getPos().angleTo(target.getPos()), 500), 500)

                new Missile(caster, caster.getPos().withZ(80), 900, caster.getPos().angleTo(target.getPos()), Abilities.crushingWaveMissile)
                    ..hitOnce()
                    ..setSize(125)
                    ..setHitCondition((Missile missile, unit target) -> (begin
                    return not target.isType(UNIT_TYPE_STRUCTURE) and target.isTargetable(missile.getOwner().getOwner(), false, true)
                end) )
                    ..setHitFunction((Missile missile, unit target) -> (begin
                    let missileCaster = missile.getOwner()
                    missileCaster.dealDamage(target, (TIDAL_FURY_WAVE_DMG[level-1]).bonus(missileCaster.getInt(), TIDAL_FURY_WAVE_DMG_BONUS), ReductionType.MAGIC)
        
                end) )
                    ..setMaxRange(500)
    else if(spellId==ABILITY_GIANT_FISH)
        ArtilleryDetection.fireArtillery(caster.getOwner(), FISH_ARTILLERY, caster.getPos(), vec2(GetSpellTargetX(), GetSpellTargetY()))

    else if spellId == ABILITY_MANA_SHIELD
        caster.addBuff(caster, -1, buffManaShield, level)

    if(spellId == ABILITY_RITUAL_OF_THE_SEAS)

        new RitualOfTheSeas(caster)

class RitualOfTheSeas

    private unit caster
    private int level
    private timer checkTimer
    private int count

    private unit dummy

    construct(unit caster)
        this.count = 0
        this.caster = caster
        this.level = caster.getAbilityLevel(ABILITY_RITUAL_OF_THE_SEAS)


        this.checkTimer = getTimer()
            ..setData(this castTo int)
            ..startPeriodic(0.1, function updateStatic)

    function update()
        this.count++
        if this.count == 20
            this.count = 0

            this.caster.addEffect("Models\\PsyWave.mdx", "origin").destr()

            group nearby = CreateGroup()
            nearby.enumUnitsInRange(this.caster.getPos(), RITUAL_AOE[level-1])


            let manaDrain = RITUAL_MANA_DRAIN[level-1].bonus(caster.getInt(), RITUAL_MANA_DRAIN_BONUS)
            let hpDrain = RITUAL_HP_DRAIN[level-1].bonus(caster.getInt(), RITUAL_HP_DRAIN_BONUS)

            while(not nearby.isEmpty())
                unit target = FirstOfGroup(nearby)
                nearby.removeUnit(target)
                if not target.isType(UNIT_TYPE_STRUCTURE) and target.isTargetable(this.caster.getOwner(), false, true) and target.isVisible(this.caster.getOwner())
                    doAfter(target.getPos().distanceTo(caster.getPos())/1000 , () -> (begin

                        let manaDrained = min( target.getMana(), manaDrain)
                        let hpDrained = min( target.getHP(), hpDrain)

                        if target.isType(UNIT_TYPE_HERO)
                            caster.addMana(manaDrained.reduceMagic(target.getWp()) * RITUAL_MANA_RATIO/100 )
                            caster.addHP( hpDrained.reduceMagic(target.getWp()) * RITUAL_HP_RATIO/100 )

                        target.addMana(-manaDrained.reduceMagic(target.getWp()))
                        caster.dealDamage(target, hpDrained, ReductionType.MAGIC)


                    end))
                    
            nearby.destr()

            return

        if this.caster.getCurrentOrder()!= OrderId("tranquility")
            destroy this
            return
    
    static function updateStatic()
        (GetExpiredTimer().getData() castTo RitualOfTheSeas).update()

    ondestroy

        this.checkTimer.release()

function initHeroType()
    setupStats()
    heroType
        ..setBonusCalculation((UnitWrapper whichWrapper, Stat whichStat) -> (begin
            let lvl = whichWrapper.toUnit().getAbilityLevel(ABILITY_ANCIENT_KNOWLEDGE)
            if lvl == 0
                UnitWrapper.defaultCalcualtion.calculate(whichWrapper, whichStat)
            else
                if whichStat == Stat.HP or whichStat == Stat.MP //if hp or mana was changed, calculate special values.
                    let hp = Stat.HP.toInt()
                    let mp = Stat.MP.toInt()
                    UnitWrapper.defaultCalcualtion.calculate(whichWrapper, Stat.HP)
                    UnitWrapper.defaultCalcualtion.calculate(whichWrapper, Stat.MP)
                    let hpBonus = whichWrapper.statsTotalBonus[hp]
                    let mpBonus = whichWrapper.statsTotalBonus[mp]
                    //print("hpBonus = "+hpBonus.toString()+", mpBonus = "+mpBonus.toString())
                    whichWrapper.statsTotalBonus[hp] = hpBonus + ((1/ANCIENT_KNOWLEDGE_HP_BONUS[lvl-1]) * (mpBonus + whichWrapper.getStatBaseValue(Stat.MP)) ).toInt()
                    whichWrapper.statsTotalBonus[mp] = mpBonus + ((1/ANCIENT_KNOWLEDGE_MP_BONUS[lvl-1]) * (hpBonus + whichWrapper.getStatBaseValue(Stat.HP)) ).toInt()
                    //print("newHpBonus = "+whichWrapper.statsTotalBonus[hp].toString()+", newHpBonus = "+whichWrapper.statsTotalBonus[mp].toString())
                    whichWrapper.applyTotalStatBonus(Stat.HP)
                    whichWrapper.applyTotalStatBonus(Stat.MP)
                else //else calculate default values
                    UnitWrapper.defaultCalcualtion.calculate(whichWrapper, whichStat)
        end))
        ..addOnSpellCastFunc(function onSpellCast)

    buffFish = new BuffType(false, true)

    var abilityList = new LinkedList<int>
        ..add(FISH_ABILTIY_ID)
    buffFish.setup(abilityList, FISH_BUFF_ID)

    buffFish.addFirstFunction( (Buff whichBuff) -> (begin
        let level = whichBuff.getLevel()
        whichBuff.getTarget().wrapper()
            ..addStatAbsoluteBonus(Stat.AS, -FISH_ATTACK_SPEED[level - 1])
            ..addStatRelativeBonus(Stat.MS, -FISH_MOVE_SPEED[level - 1]*10)
    end))

    buffFish.addEndFunction( (Buff whichBuff) -> (begin
        let level = whichBuff.getLevel()
        whichBuff.getTarget().wrapper()
            ..addStatAbsoluteBonus(Stat.AS, FISH_ATTACK_SPEED[level - 1])
            ..addStatRelativeBonus(Stat.MS, FISH_MOVE_SPEED[level - 1]*10)
    end))

    ArtilleryDetection.registerArtilleryHit(FISH_IMPACT, (unit impact) ->(begin
        group nearby = CreateGroup()
        let caster = Hero.getHero(impact.getOwner()).toUnit()
        let level = caster.getAbilityLevel(ABILITY_GIANT_FISH)
        nearby.enumUnitsInRange(impact.getPos(), FISH_AOE[level-1])

        let dmg = FISH_DAMAGE[level-1].bonus(caster.getInt(), FISH_DAMAGE_BONUS)

        while(not nearby.isEmpty())
            unit target = FirstOfGroup(nearby)
            nearby.removeUnit(target)
            if not target.isType(UNIT_TYPE_STRUCTURE) and target.isTargetable(impact.getOwner(), false, true)

                caster.dealDamage(target, dmg, ReductionType.MAGIC)
                target.addBuffReduced(caster, FISH_SLOW_DURATION[level-1], buffFish, level)

        nearby.destr()

        let fx = new Fx(impact.getPos(), Abilities.stampedeMissileDeath)
        fx.setScale(3)
        destroy fx

        SOUND_FISH_IMPACT.playOnPoint(impact.getPos3Zero()).snd.setVolume(120)

    end))

    buffManaShield = new BuffType(true, true) // the actual effect is in OnAttack
        ..setPeriodicTime(0.1)
    buffManaShield.addPeriodicFunction( (Buff whichBuff) -> (begin
        let caster = whichBuff.getCaster()
        if not caster.hasAbility(MANA_SHIELD_BUFF_ID)
            whichBuff.dispel()
    end))

init
    heroType = new HeroType(HERO_SEA_DRUID)
        ..setOnInitFunction(() -> initHeroType())
      

   