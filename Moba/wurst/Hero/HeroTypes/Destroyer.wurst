package Destroyer

//13

import HeroType
import BuffObjEditing
import UnitTypeConfig
import ClosureTimers
import DamageDetection

public constant HERO_DESTROYER = 'H00R'
HeroType heroType = null

public constant ABILITY_DEATH_FRENZY = 'AY3P'
constant ABILITY_BREATH = 'AY3Q'
constant ABILITY_IMMOLATION = 'AY3W'
constant ABILITY_BONE_WALL = 'AY3E'
constant ABILITY_WILD_RAGE = 'AY3R'

unit heroDestroyer = null
group hitByBreath = CreateGroup()

public constant real array DEATH_FRENZY_BONUS = [25, 35, 45, 55, 65]

function passive(int abilityId)
    if compiletime
        createNoEffectAbiltiy(abilityId)
            ..setupIcon("PASbloodpresence")
            ..setupWithHotKey("Death Frenzy", "D")


    let tooltip = new DynamicTooltip(abilityId)
    tooltip.setReplaceIntelligence(0)
    tooltip.setLearnString("The Destroyer deals increased damage from all sources, the more injured he becomes.", (int lvl) -> (begin
        return "up to {0}% increased damage.".format(
            DEATH_FRENZY_BONUS[lvl-1].str())
    end) )
    
    for lvl = 1 to 5
        tooltip.setNormalString(false, lvl, "The Destroyer deals up to {0}% increased damage from all sources, the more injured he becomes".format( 
            DEATH_FRENZY_BONUS[lvl-1].str())
        )
    addDynamicTooltipIntelligence(heroType, tooltip)


constant integer BUFF_ID_ON_BREATH = compiletime(createDummyBuff())

constant real array BREATH_DAMAGE = [75, 120, 165, 210, 255]
constant real BREATH_DAMAGE_BONUS = 80

constant int array BREATH_ARMOR_REDUCTION = [30, 35, 40, 45, 50]
constant real BREATH_DURATION = 4

BuffType buffMeltingBreath

constant int BREATH_BUFF_ID = compiletime(createAuraBuff("Melting Breath", "This unit was hit by Melting Breath; it has reduced armor.", "BTNFlameBurst"))
constant int BREATH_ABILITY_ID = compiletime(createNegativeBuffAbility(5, BREATH_BUFF_ID, Abilities.lordofFlameMissile, "chest"))


function abilityQ(int abilityId)

    if compiletime
        int array MANA = [80, 80, 80, 80, 80]
        real array COOLDOWN = [8, 8, 8, 8, 8]
        real array RANGE = [450, 450, 450, 450, 450]
        let aDef = new AbilityDefinitionBreathofFrostCreep(abilityId)
            ..setupIcon("BTNFlameBurst")
            ..setupWithHotKey("Melting Breath", "Q")
            ..setAnimationNames("spell,channel")
            ..setMissileArt(Abilities.breathOfFireMissile)

        for lvl = 1 to 5
            aDef
                ..setCooldown(lvl, COOLDOWN[lvl-1])
                ..setManaCost(lvl, MANA[lvl-1])
                ..setCastRange(lvl, RANGE[lvl-1])
                ..setTargetsAllowed(lvl, "ground,enemy,nonancient,neutral,organic,air")
                ..setDamage(lvl, 0.01)
                ..setDamagePerSecond(lvl, 0.01)
                ..setBuffs(lvl, BUFF_ID_ON_BREATH.toRawCode())
                ..setDistance(lvl, 375)
                ..setFinalArea(lvl, 300)
                ..setMaxDamage(lvl, 99999)
                ..setAreaofEffect(lvl, 125)

    let tooltip = new DynamicTooltip(abilityId)
    tooltip.setReplaceIntelligence(BREATH_DAMAGE_BONUS)
    tooltip.setLearnString("Breathes a cone of fire at enemy units, dealing magic damage. The heat melts the target's armor, reducing it for {0} seconds.".format(
        BREATH_DURATION.str()
    ), (int lvl) -> (begin
        return "{0}{I0} damage, {1}% armor reduction.".format(
        BREATH_DAMAGE[lvl-1].str(), BREATH_ARMOR_REDUCTION[lvl-1].str())
    end) )
    
    for lvl = 1 to 5
        tooltip.setNormalString(true, lvl, "Breathes a cone of fire at enemy units, dealing {0}{I0} magic damage. The heat melts the target's armor, reducing it by {1}% for {2} seconds.".format(
            BREATH_DAMAGE[lvl-1].str(), BREATH_ARMOR_REDUCTION[lvl-1].str(), BREATH_DURATION.str()
            ))
    addDynamicTooltipIntelligence(heroType, tooltip)


constant real array IMMOLATION_DAMAGE = [32, 44, 56, 68, 80]
constant real IMMOLATION_DAMAGE_BONUS = 30

constant int array IMMOLATION_ATTACK_SPEED = [18, 26, 34, 42, 50]
constant real array IMMOLATION_MANA_DRAIN = [5, 6.2, 7.4, 8.6, 9.8]

BuffType buffImmolation
constant IMMOLATION_BUFF_ID = compiletime(BUFF_ID_GEN.next())


function abilityW(int abilityId)

    if compiletime
        int array MANA = [25, 25, 25, 25, 25]
        real AOE = 250
        new BuffDefinition(IMMOLATION_BUFF_ID, 'BEim')
            ..setTooltipNormalExtended(1, "This unit has Immolation; it attacks faster and nearby enemy units will take damage over time.")
            ..setArtSpecial(1, Abilities.immolationRedDamage)
            ..setArtTarget(1, Abilities.immolationREDTarget)
            ..setTargetAttachmentPoint0(1, "chest")

        let aDef = new AbilityDefinitionDemonHunterImmolation(abilityId)
            ..setupIcon(Icons.bTNImmolationOn)
            ..setIconTurnOff(Icons.bTNImmolationOff)
            ..setupWithHotKey("Immolation", "W")
            ..setTurnOff("Deactivate Immolation","W")
            ..setAnimationNames("spell")

        for lvl = 1 to 5
            aDef
                ..setCooldown(lvl, 0)
                ..setManaCost(lvl, MANA[lvl-1])
                ..setAreaofEffect(lvl, AOE)
                ..setManaDrainedperSecond(lvl, IMMOLATION_MANA_DRAIN[lvl-1])
                ..setBuffs(lvl, IMMOLATION_BUFF_ID.toRawCode()+",BEia")
                ..setTargetsAllowed(lvl, "ground,enemy,nonancient,neutral,organic,air")

    let tooltip = new DynamicTooltip(abilityId)
    tooltip.setReplaceIntelligence(IMMOLATION_DAMAGE_BONUS)
    tooltip.setLearnString("Engulfs the Destroyer in flames, damaging nearby enemies and the Destroyer himself. Heroes take double damage. Also increases the Destroyer's attack speed.|nDrains mana until deactivated.", (int lvl) -> (begin
        return "{0}{I0} damage per second, {1}% attack speed.".format(
        IMMOLATION_DAMAGE[lvl-1].str(),
        IMMOLATION_ATTACK_SPEED[lvl-1].str())
    end) )
    for lvl = 1 to 5
        tooltip.setNormalString(false, lvl, "Engulfs the Destroyer in flames, dealing {0}{I0} damage per second to nearby enemies and the Destroyer himself. Heroes take double damage. Also increases the Destroyer's attack speed by {1}%.|nDrains mana until deactivated.".format(
            IMMOLATION_DAMAGE[lvl-1].str(),
            IMMOLATION_ATTACK_SPEED[lvl-1].str()))
        tooltip.setTurnOffString(false, lvl, "Deactivate to stop draining mana and life.")
    addDynamicTooltipIntelligence(heroType, tooltip)


constant int array WALL_NUMBER = [6, 7, 8, 9, 10]
constant real array WALL_DURATION = [4, 4.5, 5, 5.5, 6]

function abilityE(int abilityId)

    if compiletime
        int array MANA = [90, 85, 80, 75, 70]
        real array COOLDOWN = [17, 16, 15, 14, 13]
        real array RANGE = [1000, 1000, 1000, 1000, 1000]
        let aDef = new AbilityDefinitionIllidanChannel(abilityId)
            ..setAnimationNames("spell")
            ..setupIcon("BTNNecromancy")
            ..setupWithHotKey("Bone Wall", "E")
            ..setupChannel(TargetType.POINT, "ward", 0.5)

        for lvl = 1 to 5
            aDef
                ..setCastRange(lvl, RANGE[lvl-1])
                ..setCooldown(lvl, COOLDOWN[lvl-1])
                ..setManaCost(lvl, MANA[lvl-1])

    let tooltip = new DynamicTooltip(abilityId)
    tooltip.setReplaceIntelligence(0)
    tooltip.setLearnString("Summons a wall of bones at the target location, blocking the way for the Destroyer's victims to flee. The wall gets bigger on higher levels.", (int lvl) -> (begin
        return "Lasts {0} seconds.".format(
            WALL_DURATION[lvl-1].toExactString())
    end) )
    
    for lvl = 1 to 5
        tooltip.setNormalString(true, lvl, "Summons a wall of bones at the target location, blocking the way for the Destroyer's victims to flee. Lasts {0} seconds.".format(
            WALL_DURATION[lvl-1].toExactString()
        ))
    addDynamicTooltipIntelligence(heroType, tooltip)


        
constant real array RAGE_SELF_DAMAGE = [30, 40, 50, 60, 70]

constant real RAGE_DURATION = 15
constant real RAGE_DURATION_BONUS = 4

constant int array RAGE_DAMAGE_BONUS = [40, 50, 60, 70, 80]

public constant real RAGE_LIFE_STEAL = 0.35

public constant int RAGE_BUFF_ID = compiletime(createAuraBuff("Wild Rage", "This unit has increased damage and is healed for dealing damage. It takes damage over time.", Icons.bTNUnholyFrenzy))
constant int RAGE_ABILITY_ID = compiletime(createPositiveBuffAbility(5, RAGE_BUFF_ID, Abilities.unholyFrenzyTarget, "overhead"))

constant real array PREY_BONUS_DAMAGE = [30, 40, 50, 60, 70]
constant real PREY_DURATION = 15
constant int array PREY_MOVE_SPEED = [20, 25, 30, 35, 40]

constant int PREY_SLOW = 50
constant real PREY_SLOW_DURATION = 1

BuffType buffPreyTarget
BuffType buffPreyCaster
BuffType buffPreySlow

constant int PREY_TARGET_BUFF_ID = compiletime(createAuraBuff("Prey", "This unit is the Prey; it is revealed and takes increased damage from the Hunter.", Icons.bTNUnholyFrenzy))
constant int PREY_TARGET_ABILITY_ID = compiletime(createNegativeBuffAbility(5, PREY_TARGET_BUFF_ID, Abilities.possessionTarget, "overhead"))

constant int PREY_SLOW_BUFF_ID = compiletime(createAuraBuff("Prey - Slowed", "The Prey was hit by the Hunter; it has reduced movement speed.", Icons.bTNUnholyFrenzy))
constant int PREY_SLOW_ABILITY_ID = compiletime(createNegativeBuffAbility(5, PREY_SLOW_BUFF_ID, "", ""))

constant int PREY_CASTER_BUFF_ID = compiletime(createAuraBuff("Hunter", "This unit is the Hunter; it's movement speed is increased while moving towards the target.", Icons.bTNUnholyFrenzy))
constant int PREY_CASTER_ABILITY_ID = compiletime(createPositiveBuffAbility(5, PREY_CASTER_BUFF_ID, Abilities.unholyFrenzyTarget, "overhead"))

BuffType buffRage

function abilityR(int abilityId)

    if compiletime
        int array MANA = [140, 145, 150, 155, 160]
        real array COOLDOWN = [75, 75, 75, 75, 75]
        real RANGE = 900
        let aDef = new AbilityDefinitionIllidanChannel(abilityId)
            ..setAnimationNames("spell")
            ..setupChannel(TargetType.UNIT, "channel", 0.5)
            ..setupIcon(Icons.bTNUnholyFrenzy)
            ..setupWithHotKey("Hunter and Prey", "R")
            ..setLightningEffects("")
            ..setArtTarget("")
            ..setMissileArt("")
            ..setArtSpecial("")
            ..setLightningEffects("")
            ..setCheckDependencies(true)

        for lvl = 1 to 5
            aDef
                ..setCooldown(lvl, COOLDOWN[lvl-1])
                ..setManaCost(lvl, MANA[lvl-1])
                ..setTargetsAllowed(lvl, "ground,enemy,nonancient,neutral,organic,air")
                ..setCastRange(lvl, RANGE)


    let tooltip = new DynamicTooltip(abilityId)
    tooltip.setReplaceIntelligence(0)
    tooltip.setLearnString("Marks the target enemy unit as his prey. The Destroyer will have increased movmement speed when moving towards the prey. Every attack against the prey deals bonus damage and slows the prey. Lasts {0} seconds.".format(PREY_DURATION.toExactString()), (int lvl) -> (begin
        return "{0} damage, {1}% movement speed".format(
            PREY_BONUS_DAMAGE[lvl-1].str(),
            PREY_MOVE_SPEED[lvl-1].str())
    end) )
    
    for lvl = 1 to 5
        tooltip.setNormalString(true, lvl, "Marks the target enemy unit as his prey. The Destroyer will have move {2}% faster when moving towards the prey. Every attack against the prey deals {1} bonus damage and slows the prey. Lasts {0} seconds.".format(
            PREY_DURATION.str(),
            PREY_BONUS_DAMAGE[lvl-1].str(),
            PREY_MOVE_SPEED[lvl-1].str())
        )
    addDynamicTooltipIntelligence(heroType, tooltip)



function setupStats()
    heroType
        //..setStat(Stat.AD, 5, 40)
        ..setStat(Stat.AR, 32, 53)
        ..setStat(Stat.AS, 19, 53)
        ..setStat(Stat.HP, 14, 43)
        ..setStat(Stat.HR, 32, 69)
        ..setStat(Stat.MP, 21, 37)
        ..setStat(Stat.MR, 19, 45)
        ..setStat(Stat.MS, 14, 37)
        ..setStat(Stat.STR, 17, 55)
        ..setStat(Stat.AGI, 33, 49)
        ..setStat(Stat.INT, 11, 33)

function onDamage()
    let target = GetTriggerUnit()
    if target.hasAbility(BUFF_ID_ON_BREATH)
        target.removeAbility(BUFF_ID_ON_BREATH)

        let caster = GetEventDamageSource()
        if target.isTargetable(caster.getOwner(), false, true) and not hitByBreath.contains(target)
            let level = caster.getAbilityLevel(ABILITY_BREATH)
            hitByBreath.addUnit(target)
            caster.dealDamage(target, BREATH_DAMAGE[level-1].bonus(caster.getInt(), BREATH_DAMAGE_BONUS), ReductionType.MAGIC)
            target.addBuffReduced(caster, BREATH_DURATION, buffMeltingBreath, level)



function onAttack(unit attacker, unit target, boolean _isActive)

    let prey = BuffList.getBuffOfType(target, buffPreyTarget)
    if prey != null
        let level = prey.getLevel()
        attacker.dealDamage(target, PREY_BONUS_DAMAGE[level-1], ReductionType.PHYSICAL)
        target.addBuffReduced(attacker, PREY_SLOW_DURATION, buffPreySlow, 1)
        target.addEffect(Abilities.stampedeMissileDeath, "origin")


function onSpellCast()
    let spellId = GetSpellAbilityId()
    let caster = GetTriggerUnit()
    let level = caster.getAbilityLevel(spellId)

    if(spellId==ABILITY_BREATH)
        /*let target = GetSpellTargetUnit()
        target.addBuffReduced(caster, PREY_DURATION, buffPreyTarget, level)
        caster.addBuff(caster, -1, buffPreyCaster, level)*/
        hitByBreath.clear()

    if(spellId==ABILITY_IMMOLATION)
        caster.addBuff(caster, -1, buffImmolation, 1)

    if(spellId==ABILITY_BONE_WALL)
        let pos = vec2(GetSpellTargetX(), GetSpellTargetY())
        let angle1 = caster.getPos().angleTo(pos)
        let angle2 = angle1.op_minus(angle(bj_PI/2))
        let facing = angle1
        let modelPath = "Doodads\\LordaeronSummer\\Props\\SkullPile\\SkullPile0.mdl"

        var wall = createUnit(caster.getOwner(), WALL, pos, angle(0))
        wall.setXY(pos)
        var model = addEffect(modelPath, pos)
        model.setRoll(facing)
        doAfter(WALL_DURATION[level-1]) ->
            doPeriodicallyCounted(0.05, 20, (CallbackCounted cb) -> begin
                model.setAlpha((12.75 * cb.getCount()).round() )
            end)
            addEffect(Abilities.mirrorImageDeathCaster, pos).destr()
            model.destr()
            wall.kill()

        for i = 1 to WALL_NUMBER[level-1]
            var newPos = pos.polarOffset(angle2, 64. * i)
            wall = createUnit(caster.getOwner(), WALL, newPos, angle(0))
            wall.setXY(newPos)
            model = addEffect(modelPath, newPos)
            model.setRoll(facing)
            doAfter(WALL_DURATION[level-1]) ->
                doPeriodicallyCounted(0.05, 20, (CallbackCounted cb) -> begin
                    model.setAlpha((12.75 * cb.getCount()).round() )
                end)
                addEffect(Abilities.mirrorImageDeathCaster, newPos).destr()
                model.destr()
                wall.kill()

            newPos = pos.polarOffset(angle2, -64. * i)
            wall = createUnit(caster.getOwner(), WALL, newPos, angle(0))
            wall.setXY(newPos)
            model = addEffect(modelPath, newPos)
            model.setRoll(facing)
            doAfter(WALL_DURATION[level-1]) ->
                doPeriodicallyCounted(0.05, 20, (CallbackCounted cb) -> begin
                    model.setAlpha((12.75 * cb.getCount()).round() )
                end)
                addEffect(Abilities.mirrorImageDeathCaster, newPos).destr()
                model.destr()
                wall.kill()
    

    if(spellId==ABILITY_WILD_RAGE)
        let target = GetSpellTargetUnit()
        target.addBuffReduced(caster, PREY_DURATION, buffPreyTarget, level)
        caster.addBuff(caster, -1, buffPreyCaster, level)
        


function retreatAI(HeroAI _ai)

function attackAI(HeroAI _ai)

function teamFightAI(HeroAI _ai)
            
@compiletime
function setupTooltips()

    passive(ABILITY_DEATH_FRENZY)
    abilityQ(ABILITY_BREATH)
    abilityW(ABILITY_IMMOLATION)
    abilityE(ABILITY_BONE_WALL)
    abilityR(ABILITY_WILD_RAGE)


function initHeroType()
    setupStats()
    setupTooltips()
    heroType.setSkillBuild("QWQWQWQWQWEEEEE")
    delayInit(5) ->
        heroType
            ..setOnAttackFunc((unit attacker, unit target, boolean isActive, boolean advanced) -> onAttack(attacker, target, isActive))
            ..addOnSpellCastFunc(function onSpellCast)
            ..setRetreatAI((HeroAI ai) -> (retreatAI(ai)))
            ..setAttackAI((HeroAI ai) -> (attackAI(ai)))
            ..setTeamFightAI((HeroAI ai) -> (teamFightAI(ai)))
            //..addItem(ITEM_ID_HEAVY_BOOTS)
            //..addItem(ITEM_ID_BLOOD_SWORD)
            //..addItem(ITEM_ID_INFERNAL_BOW)
            //..addItem(ITEM_ID_RUNIC_ARMOR)
            //..addItem(ITEM_ID_SACRIFICIAL_DAGGER)
            //..addItem(ITEM_ID_CURSED_SWORD)

        var abilityList = new LinkedList<int>
        ..add(BREATH_ABILITY_ID)
        buffMeltingBreath = new BuffType(false, true)
        buffMeltingBreath.setup(abilityList, BREATH_BUFF_ID)

        buffMeltingBreath.addFirstFunction( (Buff whichBuff) -> (begin
            whichBuff.getTarget().wrapper().addStatRelativeBonus(Stat.AR, -BREATH_ARMOR_REDUCTION[whichBuff.getLevel()-1] * 10)
        end))

        buffMeltingBreath.addEndFunction( (Buff whichBuff) -> (begin
            whichBuff.getTarget().wrapper().addStatRelativeBonus(Stat.AR, BREATH_ARMOR_REDUCTION[whichBuff.getLevel()-1] * 10)
        end))

        abilityList = new LinkedList<int>
        ..add(PREY_TARGET_ABILITY_ID)
        buffPreyTarget = new BuffType(false, true)
        buffPreyTarget.setup(abilityList, PREY_TARGET_BUFF_ID)
        buffPreyTarget.setPeriodicTime(0.2)

        buffPreyTarget.addPeriodicFunction( (Buff whichBuff) -> (begin
            let caster = whichBuff.getCaster()
            let target = whichBuff.getTarget()
            let direction = caster.getPos().angleTo(target.getPos())
            let facing = caster.getFacingAngle()
            var diffDegrees = (direction - facing).degrees() %360

            if diffDegrees < 0
                diffDegrees += 360
            if diffDegrees > 180
                diffDegrees = 360 - diffDegrees

            if diffDegrees < 90
                if whichBuff.getData() == 0
                    whichBuff.setData(1)
                    caster.wrapper().addStatRelativeBonus(Stat.MS, PREY_MOVE_SPEED[whichBuff.getLevel()-1]*10)
            else
                if whichBuff.getData() == 1
                    whichBuff.setData(0)
                    caster.wrapper().addStatRelativeBonus(Stat.MS, -PREY_MOVE_SPEED[whichBuff.getLevel()-1]*10)
                
        end))

        buffPreyTarget.addFirstFunction( (Buff whichBuff) -> (begin
            let caster = whichBuff.getCaster()
            let target = whichBuff.getTarget()
            let level = whichBuff.getLevel()
            whichBuff.setData(0)
            let direction = caster.getPos().angleTo(target.getPos())
            let facing = caster.getFacingAngle()
            var diffDegrees = (direction - facing).degrees() %360

            if diffDegrees < 0
                diffDegrees += 360
            if diffDegrees > 180
                diffDegrees = 360 - diffDegrees

            if diffDegrees < 90
                whichBuff.setData(1)
                caster.wrapper().addStatRelativeBonus(Stat.MS, PREY_MOVE_SPEED[level-1]*10)

        end))

        buffPreyTarget.addEndFunction( (Buff whichBuff) -> (begin
            let caster = whichBuff.getCaster()
            let level = whichBuff.getLevel()
            if whichBuff.getData()==1
                caster.wrapper().addStatRelativeBonus(Stat.MS, -PREY_MOVE_SPEED[level-1]*10)

            let casterBuff = BuffList.getBuffOfType(caster, buffPreyCaster)
            if casterBuff!= null
                casterBuff.dispel()
        end))

        abilityList = new LinkedList<int>
        ..add(PREY_CASTER_ABILITY_ID)
        buffPreyCaster = new BuffType(true, true)
        buffPreyCaster.setup(abilityList, PREY_CASTER_BUFF_ID)

        abilityList = new LinkedList<int>
        ..add(PREY_SLOW_ABILITY_ID)
        buffPreySlow = new BuffType(true, true)
        buffPreySlow.setup(abilityList, PREY_SLOW_BUFF_ID)

        buffPreySlow.addFirstFunction( (Buff whichBuff) -> (begin
            whichBuff.getTarget().wrapper().addStatRelativeBonus(Stat.MS, -PREY_SLOW * 10)
        end))

        buffPreySlow.addEndFunction( (Buff whichBuff) -> (begin
            whichBuff.getTarget().wrapper().addStatRelativeBonus(Stat.MS, PREY_SLOW * 10)
        end))

        buffImmolation = new BuffType(true, true)
        buffImmolation.setPeriodicTime(0.2)

        buffImmolation.addFirstFunction( (Buff whichBuff) -> (begin
            let level = whichBuff.getLevel()
            whichBuff.setData(level)
            heroDestroyer.wrapper().addStatAbsoluteBonus(Stat.AS, IMMOLATION_ATTACK_SPEED[level-1])
        end))

        buffImmolation.addEndFunction( (Buff whichBuff) -> (begin
            let level = whichBuff.getData()
            heroDestroyer.wrapper().addStatAbsoluteBonus(Stat.AS, -IMMOLATION_ATTACK_SPEED[level-1])
        end))

        buffImmolation.addPeriodicFunction((Buff whichBuff) -> (begin
            let owner = heroDestroyer.getOwner()
            let level = whichBuff.getData()
            if heroDestroyer.hasAbility(IMMOLATION_BUFF_ID)
                let dmg = IMMOLATION_DAMAGE[level-1].bonus(heroDestroyer.getInt(), IMMOLATION_DAMAGE_BONUS) * 0.2
                //use hp to not trigger damage event
                heroDestroyer.addHP(-(0.5 * dmg.reduceMagic(heroDestroyer.getWp())))
                group g = CreateGroup()
                g.enumUnitsInRange(heroDestroyer.getPos(), 250)
                while true
                    let u = FirstOfGroup(g)
                    if u == null
                        break
                    g.removeUnit(u)
                    if not u.isType(UNIT_TYPE_STRUCTURE) and u.isTargetable(owner, false, true)
                        if u.isType(UNIT_TYPE_HERO)
                            heroDestroyer.dealDamage(u, 2 * dmg, ReductionType.MAGIC)
                        else
                            heroDestroyer.dealDamage(u, dmg, ReductionType.MAGIC)
            else
                whichBuff.dispel()
        end))


        abilityList = new LinkedList<int>
        ..add(RAGE_ABILITY_ID)
        buffRage = new BuffType(true, true)
        buffRage.setup(abilityList, RAGE_BUFF_ID)

        buffRage.setPeriodicTime(0.2)
        buffRage.addPeriodicFunction((Buff whichBuff) -> (begin
            //use hp to not trigger damage event
            heroDestroyer.addHP(-0.2 * (RAGE_SELF_DAMAGE[whichBuff.getLevel()-1].reduceMagic(heroDestroyer.getInt())))
        end))

        buffRage.addFirstFunction( (Buff whichBuff) -> (begin
            heroDestroyer.wrapper().addStatAbsoluteBonus(Stat.AD, RAGE_DAMAGE_BONUS[whichBuff.getLevel()-1])
        end))

        buffRage.addEndFunction( (Buff whichBuff) -> (begin
            heroDestroyer.wrapper().addStatAbsoluteBonus(Stat.AD, -RAGE_DAMAGE_BONUS[whichBuff.getLevel()-1])
        end))


        addOnDamageFunc( Condition (function onDamage))
    
init
    
    heroType = new HeroType(HERO_DESTROYER, "BTNEligor", 13, false)
        ..setOnInitFunction(() -> initHeroType())
        ..setOnCreateFunction((Hero h) -> begin
        heroDestroyer = h.toUnit()
    end)
        
