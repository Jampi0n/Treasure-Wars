package PythonWarriores

//0

import HeroType
import HeroControl
import Stats
import OnAttack
import LinkedList
import Buff
import General
import BuffTypeConfig
import ClosureTimers
import OnUnitEnterLeave
import SoundUtils
import Assets
import AbilityObjEditing
import HeroObjectEditing
import ObjectIdGenerator
import ObjectIds

constant SOUND_PARALYZING_STRIKE = new SoundDefinition(Sounds.shadowStrikeBirth1, false, true)
constant SOUND_POISON_SPIT = new SoundDefinition(Sounds.corrosiveBreathMissileTarget1, false, true)
constant SOUND_EMPOWERED_STRIKE = new SoundDefinition(Sounds.metalHeavySliceFlesh3, false, true)

constant ABILITY_ENVENOMED_BLADES = 'AZ0P'
constant ABILITY_MISTWALK = 'AZ0Q'
constant ABILITY_POISON_SPIT = 'AZ0W'
constant ABILITY_EMPOWERED_STRIKE = 'AZ0E'
constant ABILITY_PARALYZING_STRIKE = 'AZ0R'

public constant HERO_PYTHON_WARRIORES = 'H00J'
HeroType heroType

//Balancing and Object Editing
constant real array ENVENOMED_BLADES_DAMAGE = [5, 10, 15, 20, 25]
constant real ENVENOMED_BLADES_DAMAGE_BONUS = 20
constant real array ENVENOMED_BLADES_DURATION = [5, 5 ,5 ,5 ,5 ]
constant BUFF_ENVENOMED_BLADES = new BuffType(false, true)



@compiletime
function envenomedBlades()
    let aDef = createNoEffectAbiltiy(ABILITY_ENVENOMED_BLADES)
        ..presetIcon("PASPoisonBlades")
        ..setupWithHotKey("Envenomed Blades", "D")
        ..setLearnString("Attacks deal poison damage every second.", (int lvl) -> (begin
            return "{0} damage, {1} seconds.".format(
            ENVENOMED_BLADES_DAMAGE[lvl-1].str()+ENVENOMED_BLADES_DAMAGE_BONUS.getBonusString(C_INT), 
            ENVENOMED_BLADES_DURATION[lvl-1].str())
        end) )

    for lvl = 1 to 5
        aDef
            ..setTooltipNormalExtended(lvl, "Attacks deal {0} poison damage every second for {1} seconds.".format(
            ENVENOMED_BLADES_DAMAGE[lvl-1].str()+ENVENOMED_BLADES_DAMAGE_BONUS.getBonusString(C_INT), 
            ENVENOMED_BLADES_DURATION[lvl-1].str())
            )
    
constant real array MISTWALK_DURATION = [4, 5, 6, 7, 8]
constant real MISTWALK_DURATION_BONUS = 4

@compiletime
function mistwalk()
    int array MANA = [80, 90, 100, 110, 120]
    real array COOLDOWN = [15, 15, 15, 15, 15]

    let aDef = new AbilityDefinitionRoar(ABILITY_MISTWALK)
        ..presetIcon("BTNNoxiousFumes")
        ..setAnimationNames("morph,first")
        ..setupWithHotKey("Mistwalk", "Q")
        ..setLightningEffects("")
        ..setArtCaster("")
        ..setArtEffect("")
        ..setMissileArt("")
        ..setArtTarget("")
        ..setLearnString("Turns the Python Warriores invisibile.", (int lvl) -> (begin
            return "{0} seconds.".format( MISTWALK_DURATION[lvl-1].str()+MISTWALK_DURATION_BONUS.getBonusString(C_INT))
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setTargetsAllowed(lvl, "none")
            ..setTooltipNormalExtended(lvl,
                "Turns the Python Warriores invisibile for {0} seconds.".format(
                MISTWALK_DURATION[lvl-1].toInt().toString()+MISTWALK_DURATION_BONUS.getBonusString(C_INT)))

constant real array POISON_SPIT_SLOW_MS = [-0.3, -0.35, -0.4, -0.45, -0.5]
constant real array POISON_SPIT_DOT = [10, 15, 20, 25, 30]
constant real POISON_SPIT_DOT_BONUS = 25.
constant int array POISON_SPIT_ARMOR_REDUCTION = [-5, -10, -15, -20, -25]
constant real array POISON_SPIT_DURATION = [6, 7, 8, 9, 10]

constant integer ABILITY_POISON_SPIT_SLOWED = compiletime(ABIL_ID_GEN.next())
constant integer ABILITY_POISON_SPIT_ARMOR_REDUCED = compiletime(ABIL_ID_GEN.next())
constant integer BUFF_ID_POISON_SPIT = 'B00B'

constant BUFF_POISON_SPIT = new BuffType(false, true)

@compiletime
function poisonSpit()
    int array MANA = [110, 120, 130, 140, 160]
    real array COOLDOWN = [16, 16, 16, 16, 16]

    let aDef = new AbilityDefinitionDarkRangerSilence(ABILITY_POISON_SPIT)
        ..presetIcon("BTNSlowSpellGreen")
        ..setAnimationNames("spell")
        ..setupWithHotKey("Poison Spit", "W")
        ..setLightningEffects("")
        ..setArtCaster("")
        ..setArtEffect("")
        ..setMissileArt("")
        ..setArtTarget("")
        ..setLearnString("Spits poison at the target area. Enemies within the area of effect are slowed, have reduced armor and take damage over time.",
        (int lvl) -> (begin
            return "{0} damage per second, {1} slow, {2} armor, lasts {3} seconds.".format(
            POISON_SPIT_DOT[lvl-1].str()+POISON_SPIT_DOT_BONUS.getBonusString(C_INT), 
            (-POISON_SPIT_SLOW_MS[lvl-1]*100).str()+"%",
            (-POISON_SPIT_ARMOR_REDUCTION[lvl-1]).str(),
            POISON_SPIT_DURATION[lvl-1].str())
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setTargetsAllowed(lvl, "none")
            ..setTooltipNormalExtended(lvl, "Spits poison at the target area. Enemies within the area of effect are slowed by {0}, have their armor reduced by {1} and take {2} damage per second.|nLasts {3} seconds.".format(
            (-POISON_SPIT_SLOW_MS[lvl-1]*100).str()+"%", (-POISON_SPIT_ARMOR_REDUCTION[lvl-1]).str(), POISON_SPIT_DOT[lvl-1].str()+POISON_SPIT_DOT_BONUS.getBonusString(C_INT), POISON_SPIT_DURATION[lvl-1].str()))

    let aDefSlowAura = new AbilityDefinitionAuraSlow(ABILITY_POISON_SPIT_SLOWED)
        ..setLevels(5)
        ..hideIcon()
        ..setArtTarget("Units\\Undead\\PlagueCloud\\PlagueCloudtarget.mdl")
        ..setTargetAttachmentPoint("chest")
    for lvl = 1 to 5
        aDefSlowAura
            //..setMovementSpeedFactor(lvl, POISON_SPIT_SLOW_MS[lvl-1]) movement speed will be handled via trigger
            ..setMovementSpeedFactor(lvl, 0)
            ..setTargetsAllowed(lvl, "invulnerable,self,vulnerable")
            ..setAreaofEffect(lvl, 1)
            ..setBuffs(lvl, int2fourchar(BUFF_ID_POISON_SPIT))

    let aDefArmorReduction = new AbilityDefinitionDefenseBonusPlus1(ABILITY_POISON_SPIT_ARMOR_REDUCED)
        ..setLevels(5)
        ..hideIcon()
        ..setItemAbility(false)
    for lvl = 1 to 5
        aDefArmorReduction
            ..setDefenseBonus(lvl, POISON_SPIT_ARMOR_REDUCTION[lvl-1])

constant real array EMPOWERED_STRIKE_DAMAGE = [50, 75, 100, 125, 150]
constant real EMPOWERED_STRIKE_DAMAGE_BONUS = 100

@compiletime
function empoweredStrike()
    int array MANA = [50, 50, 50, 50, 50]
    real array COOLDOWN = [6, 6, 6, 6, 6]

    let aDef = new AbilityDefinitionSeaWitchForkedLightning(ABILITY_EMPOWERED_STRIKE)
        ..presetIcon("BTNEnchantWeaponGreen")
        ..setAnimationNames("spell,throw")
        ..setupWithHotKey("Empowered Strike", "E")
        ..setLightningEffects("")
        ..setArtSpecial("")
        ..setLightningEffects("")
        ..setLearnString("Strikes the target enemy unit with a powerful melee attack dealing physical damage.", (int lvl) -> (begin
            return "{0} damage.".format( EMPOWERED_STRIKE_DAMAGE[lvl-1].str()+EMPOWERED_STRIKE_DAMAGE_BONUS.getBonusString(C_STR))
        end) )

    for lvl = 1 to 5
        aDef
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setNumberofTargetsHit(lvl, 1)
            ..setDamageperTarget(lvl, 0)
            ..setCastRange(lvl, 150)
            ..setDurationNormal(lvl, 0.1)
            ..setTargetsAllowed(lvl, "air,enemies,ground,neutral,nonancient,organic")
            ..setTooltipNormalExtended(lvl, "Strikes the target enemy unit with a powerful melee attack dealing {0} physical damage.".format( 
            EMPOWERED_STRIKE_DAMAGE[lvl-1].str()+EMPOWERED_STRIKE_DAMAGE_BONUS.getBonusString(C_STR)))



constant real array PARALYZING_STRIKE_DAMAGE = [250,300,350,400,450]
constant real PARALYZING_STRIKE_DAMAGE_BONUS = 400
constant real array PARALYZING_STRIKE_DURATION = [1.5, 1.75, 2, 2.25, 2.5]
constant real PARALYZING_STRIKE_DURATION_BONUS = 2

@compiletime
function paralyzingStrike()
    real array RANGE = [500, 600, 700, 800, 900]
    int array MANA = [180, 190, 200, 210, 220]
    real array COOLDOWN = [60, 60, 60, 60, 60]

    let aDef = new AbilityDefinitionFarseerChainLightning(ABILITY_PARALYZING_STRIKE)
        ..presetIcon("BTNINV_ThrowingKnife_04")
        ..setAnimationNames("spell, slam")
        ..setupWithHotKey("Paralyzing Strike", "R")
        ..setLightningEffects("")
        ..setMissileArt("")
        ..setArtTarget("")
        ..setLearnString("Deals massive magical damage to the target, stunning it and teleports the Python Warriores behind the target.", (int lvl) -> (begin
            return "{0} damage, {1} seconds stun.".format( PARALYZING_STRIKE_DAMAGE[lvl-1].str()+PARALYZING_STRIKE_DAMAGE_BONUS.getBonusString(C_INT),
            PARALYZING_STRIKE_DURATION[lvl-1].toString()+PARALYZING_STRIKE_DURATION_BONUS.getBonusString(C_INT))
        end) )

    for lvl = 1 to 5
        aDef
            ..setDamageperTarget(lvl, 0)
            ..setDamageReductionperTarget(lvl, 0)
            ..setNumberofTargetsHit(lvl, 0)
            ..setCastRange(lvl, RANGE[lvl-1])
            ..setCooldown(lvl, COOLDOWN[lvl-1])
            ..setManaCost(lvl, MANA[lvl-1])
            ..setTargetsAllowed(lvl, "air,enemies,ground,neutral,nonancient,organic")
            ..setTooltipNormalExtended(lvl, "Deals {0} magical damage to the target, stunning it for {1} seconds and teleports the Python Warriores behind the target.".format(
            PARALYZING_STRIKE_DAMAGE[lvl-1].str()+PARALYZING_STRIKE_DAMAGE_BONUS.getBonusString(C_INT),
            PARALYZING_STRIKE_DURATION[lvl-1].toString()+PARALYZING_STRIKE_DURATION_BONUS.getBonusString(C_INT)))




group spitDummies = CreateGroup()

function setupStats()
    heroType
        //..setStat(Stat.AD, 10, 40)
        ..setStat(Stat.AR, 10, 25)
        ..setStat(Stat.AS, 25, 60)
        ..setStat(Stat.HP, 15, 40)
        ..setStat(Stat.HR, 20, 50)
        ..setStat(Stat.MP, 10, 30)
        ..setStat(Stat.MR, 15, 25)
        ..setStat(Stat.MS, 20, 60)
        ..setStat(Stat.STR, 5+10, 20+40)
        ..setStat(Stat.WP, 20, 35)
        ..setStat(Stat.INT, 5, 40)

function onSpellCast()
    let spellId = GetSpellAbilityId()
    let caster = GetTriggerUnit()
    let level = caster.getAbilityLevel(spellId)

    if(spellId==ABILITY_MISTWALK)
        caster.dispelBuffs(true, true)
        new Buff(caster, caster, (MISTWALK_DURATION[level-1]).bonus(caster.getInt(), MISTWALK_DURATION_BONUS), INVIS, 1)
        return

    if(spellId==ABILITY_POISON_SPIT)
        let x = GetSpellTargetX()
        let y = GetSpellTargetY()
        let attacker = createUnit(caster.getOwner(), 'u000', caster.getPos(), angle(Atan2(y - caster.getY(), x - caster.getX())))
        attacker.setAbilityLevel('A00I', level)

        if(not attacker.issuePointOrder("attackground", vec2(x,y)))
            print("failed to order attackground")
        attacker.setTimedLife(1)
        return

    if(spellId==ABILITY_EMPOWERED_STRIKE)
        let target = GetSpellTargetUnit()
        caster.dealDamage(target, (EMPOWERED_STRIKE_DAMAGE[level-1]).bonus(caster.getStr(), EMPOWERED_STRIKE_DAMAGE_BONUS) , ReductionType.PHYSICAL)
        target.addEffect("Abilities\\Spells\\Other\\Stampede\\StampedeMissileDeath.mdl", "chest").destr()
        SOUND_EMPOWERED_STRIKE.playOnPoint(target.getPos3Zero()).snd.setVolume(150)
        return

    if(spellId==ABILITY_PARALYZING_STRIKE)
        let target = GetSpellTargetUnit()
        var targetPos = target.getPos()
        var casterPos = caster.getPos()
        var portPos = targetPos.polarOffset(casterPos.angleTo(targetPos), 100)
        SOUND_PARALYZING_STRIKE.playOnPoint(targetPos.toVec3())
        doAfter(0, () -> caster.setPos(portPos))
        new Buff(target, caster, (PARALYZING_STRIKE_DURATION[level-1]).bonus(caster.getInt(), PARALYZING_STRIKE_DURATION_BONUS) , STUNNED, 1)
        caster.dealDamage(target, (PARALYZING_STRIKE_DAMAGE[level-1]).bonus(caster.getInt(), PARALYZING_STRIKE_DAMAGE_BONUS) , ReductionType.MAGIC)
        return

    

function onAttack()
    let level = OnAttack.getAttacker().getAbilityLevel(ABILITY_ENVENOMED_BLADES)
    if level != 0
        
        new Buff(OnAttack.getTarget(), OnAttack.getAttacker(), ENVENOMED_BLADES_DURATION[level-1], BUFF_ENVENOMED_BLADES, level)


//in for group
function poisonSpitPeriodic()
    unit poisonSpitDummy = GetEnumUnit()
    int level = poisonSpitDummy.getTypeId()+1-'n007'
    player owner =poisonSpitDummy.getOwner()
    unit hero =  Hero.getHero(owner).toUnit()
    if(level>3)
        level = poisonSpitDummy.getTypeId()+4-'n00A'

    group nearby = CreateGroup()
    nearby.enumUnitsInRange(poisonSpitDummy.getPos(), 175.+25*level)

    while(not nearby.isEmpty())
        unit target = FirstOfGroup(nearby)
        nearby.removeUnit(target)
        if not target.isType(UNIT_TYPE_STRUCTURE) and target.isTargetable(poisonSpitDummy.getOwner(), false, true)
            new Buff(target, hero, 0.5, BUFF_POISON_SPIT, level)

    nearby.destr()

init
    heroType = new HeroType(HERO_PYTHON_WARRIORES)
    heroType.addOnAttackFunc(function onAttack)

    setupStats()

    var abilityList = new LinkedList<int>
    ..add('A009')
    BUFF_ENVENOMED_BLADES.setup(abilityList, 'B007')
    BUFF_ENVENOMED_BLADES.setPeriodicTime(1)
    BUFF_ENVENOMED_BLADES.addPeriodicFunction(()-> (begin
        let thisBuff = Buff.getTriggerBuff()
        thisBuff.getCaster().dealDamage(thisBuff.getTarget(), 
        (ENVENOMED_BLADES_DAMAGE[thisBuff.getLevel()-1]).bonus(thisBuff.getCaster().getInt(), ENVENOMED_BLADES_DAMAGE_BONUS)
        , ReductionType.MAGIC)
    end) )
    

    //Poison Spit
    abilityList = new LinkedList<int>
    ..add(ABILITY_POISON_SPIT_ARMOR_REDUCED)
    ..add(ABILITY_POISON_SPIT_SLOWED)
    BUFF_POISON_SPIT.setup(abilityList, BUFF_ID_POISON_SPIT)
    BUFF_POISON_SPIT.addApplyFunction( () -> (begin
        let thisBuff = Buff.getTriggerBuff()
        thisBuff.getCaster().dealDamage(thisBuff.getTarget(), 
        (POISON_SPIT_DOT[thisBuff.getLevel()-1]).bonus(thisBuff.getCaster().getInt(), POISON_SPIT_DOT_BONUS)/4
        , ReductionType.MAGIC)
    end) )
    BUFF_POISON_SPIT.addFirstFunction( () -> (begin
        let thisBuff = Buff.getTriggerBuff()
        print("add slow")
        thisBuff.getTarget().addMovementSpeedRelative( POISON_SPIT_SLOW_MS[thisBuff.getLevel()-1] )
    end) )

    BUFF_POISON_SPIT.addEndFunction( () -> (begin
        let thisBuff = Buff.getTriggerBuff()
        print("remove slow")
        thisBuff.getTarget().addMovementSpeedRelative( -POISON_SPIT_SLOW_MS[thisBuff.getLevel()-1] )
    end) )

    onEnter(() -> (begin
        let spitDummy = getEnterLeaveUnit()
        int level = spitDummy.getTypeId()+1-'n007'
        if(level>3)
            level = spitDummy.getTypeId()+4-'n00A'

        if level >= 1 and level <= 5
            spitDummy.setTimedLife(POISON_SPIT_DURATION[level-1])
            spitDummies.addUnit(spitDummy)
            SOUND_POISON_SPIT.playOnPoint(spitDummy.getPos3Zero())
            real x
            real y
            real angle
            for i = 0 to 9 + level*3
                angle = i/(10 + level*3)*2*bj_PI
                x = spitDummy.getX() + (140 + 25*level) * angle.cos()
                y = spitDummy.getY() + (140 + 25*level) * angle.sin()
                var e = AddSpecialEffect("Units\\Undead\\PlagueCloud\\PlagueCloudtarget.mdl", x, y)
                doAfter(5.+level, () -> DestroyEffect(e))

    end) )

    onLeave(() -> (begin
        let spitDummy = getEnterLeaveUnit()
        int level = spitDummy.getTypeId()+1-'n007'
        if(level>3)
            level = spitDummy.getTypeId()+4-'n00A'

        if level >= 1 and level <= 5
            
            spitDummies.removeUnit(spitDummy)
    end) )

    let spitPeriodicTrigger = CreateTrigger()
    spitPeriodicTrigger.registerTimerEvent(0.25, true)
    spitPeriodicTrigger.addAction(() -> (begin
        ForGroup(spitDummies, function poisonSpitPeriodic)
    end) )


    heroType.addOnSpellCastFunc(function onSpellCast)



