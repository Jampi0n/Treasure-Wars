package HeroType

import HashMap
import OnAttack
import public Stats
import public Buff
import public BuffTypeConfig
import public General
import public UnitWrapper
import public HeroObjectEditing
import public Assets
import public AbilityObjEditing
import initlater HeroControl

public interface OnCreateFunction
    function onCreate(Hero hero)

public constant HERO_MAX_LEVEL = 25

public class HeroType

    private int array[11] statsBase
    private int array[11] statsMax

    private static constant unitIdToHeroType = new HashMap<integer, HeroType>
    private int unitId
    private trigger onSpellCastTrigger = CreateTrigger()
    private OnAttackFunction onAttack = null
    private CalculateTotalBonus bonusCalculation = null

    private OnCreateFunction onCreateFunction = null

    private boolean useHeroGlow = false

    static HeroType array heroTypes 
    static int heroTypeCount = 0

    construct(int id)
        unitIdToHeroType.put(id, this)
        this.unitId = id
        for i = 0 to 10
            statsBase[i]=0
            statsMax[i]=0

        heroTypes[heroTypeCount] = this
        heroTypeCount++

    function addMorph(int id)
        unitIdToHeroType.put(id, this)

    function setBase(Stat stat, integer value)
        this.statsBase[stat.toInt()] = value

    function getBase(Stat stat) returns integer
        return this.statsBase[stat.toInt()]

    function setMax(Stat stat, integer value)
        this.statsMax[stat.toInt()] = value

    function getMax(Stat stat) returns integer
        return this.statsMax[stat.toInt()]

    function setStat(Stat stat, integer min, integer max)
        this.statsBase[stat.toInt()] = min
        this.statsMax[stat.toInt()] = max

    function getUnitId() returns integer
        return this.unitId

    function onSpellCast()
        this.onSpellCastTrigger.execute()

    function onAttack(unit attacker, unit target, boolean isActive)
        if this.onAttack != null
            this.onAttack.onAttack(attacker, target, isActive)

    function setOnAttackFunc(OnAttackFunction onAttack)
        this.onAttack = onAttack

    function addOnSpellCastFunc(code onSpellCast)
        onSpellCastTrigger.addAction(onSpellCast)    

    function getOnSpellCastTrigger() returns trigger
        return this.onSpellCastTrigger

    /** Calulates the stat of the hero type for a given level */
    function calculateStat(Stat stat, integer level) returns integer
        if level == 0
            return 0

        let result = getBase(stat) + ( ( this.getMax(stat) - getBase(stat) ) *( (level - 1 )/(HERO_MAX_LEVEL - 1) ) ).toInt()
        //print(stat.toString()+" -> "+result.toString())
        return result
    
    function setBonusCalculation(CalculateTotalBonus bonusCalculation)
        this.bonusCalculation = bonusCalculation

    function getBonusCalculation() returns CalculateTotalBonus
        return this.bonusCalculation

    static function getHeroType(integer unitId) returns HeroType
        return unitIdToHeroType.get(unitId)

    function useHeroGlow()
        this.useHeroGlow = true

    function getUsesHeroGlow() returns boolean
        return this.useHeroGlow

    function setOnCreateFunction(OnCreateFunction onCreate)
        this.onCreateFunction = onCreate

    function getOnCreateFunction() returns OnCreateFunction
        return this.onCreateFunction