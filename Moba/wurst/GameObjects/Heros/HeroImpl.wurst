package HeroImpl

import HashMap
import GameTimer
import Upgrades
import TimerUtils
import ClosureTimers
import Execute
import Constants
import LinkedList
import Stats
import UnitImpl
import IHero
import HeroTypeImpl
import TeamUtil
import UnitLib
import PlayerControl

constant HP_PER_LEVEL = 15
constant STRENGTH_BONUS = 45

constant int array ASSIST_BOUNTY = [0, 60, 40, 30, 24]
constant int array KILL_BOUNTY = [240, 180, 160, 150, 144]

string array assistStringListTeam1
string array assistStringListTeam2

constant int array LIST_ID_POW_2 = [1, 2, 4, 8, 16, 1, 2, 4, 8, 16]

function initAssistLists()
    for i = 0 to 30 // 31 would be all 5, which is not needed
        assistStringListTeam1[i] = ""
        assistStringListTeam2[i] = ""
        let indexList = new LinkedList<int>
        string array assistName1
        string array assistName2
        int size = 0
        for j = 0 to 4
            if i div LIST_ID_POW_2[j] mod 2 == 1
                indexList.add(j)
                assistName1[size] = Players.coloredName(Teams.first1() + j)
                assistName2[size] = Players.coloredName(Teams.first2() + j)
                size++

        //creating the assist string with correct use of <,> and <and>
        if size > 2
            if size == 3
                assistStringListTeam1[i] = assistName1[0]+", "+assistName1[1]+" and "+assistName1[2] + " get "
                assistStringListTeam2[i] = assistName2[0]+", "+assistName2[1]+" and "+assistName2[2] + " get "
            else // 4
                assistStringListTeam1[i] = assistName1[0]+", "+assistName1[1]+", "+assistName1[2]+" and "+assistName1[3] + " get "
                assistStringListTeam2[i] = assistName2[0]+", "+assistName2[1]+", "+assistName2[2]+" and "+assistName2[3] + " get "
        else
            if size == 1
                assistStringListTeam1[i] = assistName1[0] + " gets "
                assistStringListTeam2[i] = assistName2[0] + " gets "
            else //2
                assistStringListTeam1[i] = assistName1[0]+" and "+assistName1[1] + " get "
                assistStringListTeam2[i] = assistName2[0]+" and "+assistName2[1] + " get "



        destroy indexList
        

public class HeroImpl implements IHero

    static constant assistDuration = 10.

    private player owner
    private int ownerId
    private IHeroType heroType
    private unit hero
    private integer level
    private static constant onDamageTrigger = CreateTrigger()
    private static constant onDeathTrigger = CreateTrigger()
    private static constant onLevelUpTrigger = CreateTrigger()
    //protected static Hero array heroes
    private static constant unitToHero = new HashMap<unit, HeroImpl>
    private constant assistMap = new HashMap<int, real>
    private int lastAssistant = -1
    private timer reviveTimer
    private timerdialog reviveWindow
    private int team

    private int array [5] skillIds

    private real array [4] defaultCooldown
    //modifies each cooldown seperately
    private real array [4] cooldownMod

    //when gold or xp is shared, their values are mostly non integer. To avoid rounding errors, the remainder will be stored here.
    private real sharedXp = 0

    private unit heroGlow = null

    private timer tooltipUpdateTimer


    protected static function classInit()
        onDamageTrigger.addAction(function onDamageStatic)
        onDeathTrigger.addAction(function onDeathStatic)
        onLevelUpTrigger.addAction(function onLevelUpStatic)

        CreateTrigger()
        ..addAction(() -> (begin
            let heroUnit = GetTriggerUnit()
            
            if unitToHero.has(heroUnit)
                let hero = unitToHero.get(heroUnit)
                hero.updateCooldowns()

        end))
        ..registerAnyUnitEvent(EVENT_PLAYER_HERO_SKILL)


    private function setup()
        this.team = Players.team(this.ownerId)
        this.heroType = HeroType.get(this.hero.getTypeId())

        this.skillIds[0] = this.heroType.getAbilityId(AbilitySlot.Q)
        this.skillIds[1] = this.heroType.getAbilityId(AbilitySlot.W)
        this.skillIds[2] = this.heroType.getAbilityId(AbilitySlot.E)
        this.skillIds[3] = this.heroType.getAbilityId(AbilitySlot.R)
        this.skillIds[4] = this.heroType.getAbilityId(AbilitySlot.PASSIVE)

        for i = Teams.first1() to Teams.last1()
            assistMap.put(i, 0)
        for i = Teams.first2() to Teams.last2()
            assistMap.put(i, 0)

        for i = 0 to 3
            this.cooldownMod[i] = 0
            this.defaultCooldown[i] = 0

        execute( -> this.heroType.initialize())
        
        
        unitToHero.put(this.hero, this)
        Players.addHero(this.ownerId, this)

        onDamageTrigger.registerUnitEvent(this.hero, EVENT_UNIT_DAMAGED)
        onDeathTrigger.registerUnitEvent(this.hero, EVENT_UNIT_DEATH)
        onLevelUpTrigger.registerUnitEvent(this.hero, EVENT_UNIT_HERO_LEVEL)
            
        let wrapper = this.hero.wrapper()

        this.reviveTimer = getTimer()
        ..setData(this castTo int)
        this.reviveWindow = CreateTimerDialog(this.reviveTimer)
        TimerDialogDisplay(this.reviveWindow, false)
        TimerDialogSetTitle(this.reviveWindow, Players.coloredName(this.ownerId))

        if this.heroType.getBonusCalculation() != null
            wrapper.setBonusCalculation(this.heroType.getBonusCalculation())

        if this.heroType.getUsesHeroGlow()
            this.heroGlow = createUnit(this.hero.getOwner(), 'n00H', this.hero.getPos(), angle(0))
            doPeriodically(0.03, (CallbackPeriodic _) ->(begin
                if this.hero.isAlive()
                    this.heroGlow.setXY(this.hero.getPos())
            end))

        wrapper.modManaRegen(this.heroType.getBase(Stat.MR) * 1 )
        wrapper.modHealthRegen(this.heroType.getBase(Stat.HR) * 3 )
        wrapper.modHealth(this.heroType.getBase(Stat.HP) * 15 + HP_PER_LEVEL )
        wrapper.modMana(this.heroType.getBase(Stat.MP) * 10 )
        wrapper.modMoveSpeed(this.heroType.getBase(Stat.MS) )
        SetPlayerTechResearched(this.owner, UPG_AR , this.heroType.getBase(Stat.AR))
        SetPlayerTechResearched(this.owner, UPG_AS , this.heroType.getBase(Stat.AS))
        SetHeroStr(this.hero, this.heroType.getBase(Stat.STR) + STRENGTH_BONUS , true)
        SetHeroAgi(this.hero, this.heroType.getBase(Stat.AGI), true)
        SetHeroInt(this.hero, this.heroType.getBase(Stat.INT), true)

        wrapper.updateBaseStatsHero()

        wrapper.updateAll()
        wrapper.applyAll()
        
        if this.heroType.getOnCreateFunction() != null
            this.heroType.getOnCreateFunction().onCreate(this)
            destroy this.heroType.getOnCreateFunction()
            this.heroType.setOnCreateFunction(null)
                


        this.heroType.updateTooltipsStr(this.hero)
        this.heroType.updateTooltipsInt(this.hero)

        this.tooltipUpdateTimer = getTimer()..setData(this castTo int)

        if PlayerWrapper.revealIcons
            PlayerWrapper.revealIcon(this.ownerId)



    construct(int playerId, int unitId, vec2 pos, angle facing)
        objectCount++
        this.owner = players[playerId]
        this.ownerId = playerId
        this.level = 1
        this.hero = createUnit(this.owner, unitId, pos, facing)
        this.setup()
    
    construct(unit u)
        objectCount++
        this.hero = u
        this.level = GetHeroLevel(this.hero)
        this.owner = u.getOwner()
        this.ownerId = owner.getId()
        this.setup()

    static function copyInventory(unit copyFrom, unit copyTo)
        for i = 0 to 5
            let itm = copyTo.itemInSlot(i)
            let itmId = copyFrom.itemInSlot(i).getTypeId()
            if itm.getTypeId() != itmId
                itm.remove()
                copyTo.addItemToSlot(itmId, i)

    static function getHero(unit whichUnit) returns HeroImpl
        return unitToHero.get(whichUnit)

    override function toUnit() returns unit
        return this.hero

    protected static function regenerate()
        for i = 0 to MAX_HERO_COUNT-1
            let heroes = Players.heroList(i)
            if heroes != null
                for hero in heroes
                    let heroUnit = hero.toUnit()
                    if heroUnit.isAlive()
                        let wrapper = heroUnit.wrapper()
                        heroUnit.healHP(heroUnit, wrapper.getHealthRegen() / 100)
                        heroUnit.healMana(heroUnit, wrapper.getManaRegen() / 100)

    private function upgrade()
        let newLvl = GetHeroLevel(this.hero)
        let wrapper = this.hero.wrapper()
        SetPlayerTechResearched(this.owner, UPG_AR , this.heroType.calculateStat(Stat.AR, newLvl))
        SetPlayerTechResearched(this.owner, UPG_AS , this.heroType.calculateStat(Stat.AS, newLvl))
        wrapper.modHealth( (this.heroType.calculateStat(Stat.HP, newLvl) - this.heroType.calculateStat(Stat.HP, this.level)) * 15 )
        wrapper.modMana( (this.heroType.calculateStat(Stat.MP, newLvl) - this.heroType.calculateStat(Stat.MP, this.level)) * 10 )
        wrapper.modManaRegen( (this.heroType.calculateStat(Stat.MR, newLvl) - this.heroType.calculateStat(Stat.MR, this.level)) * 1 )
        wrapper.modHealthRegen( (this.heroType.calculateStat(Stat.HR, newLvl) - this.heroType.calculateStat(Stat.HR, this.level)) * 3 )
        wrapper.modHealth( (newLvl - this.level) * HP_PER_LEVEL )
        wrapper.modMoveSpeed( this.heroType.calculateStat(Stat.MS, newLvl) - this.heroType.calculateStat(Stat.MS, this.level))
        SetHeroStr(this.hero, this.heroType.calculateStat(Stat.STR, newLvl) + STRENGTH_BONUS , true)
        SetHeroAgi(this.hero, this.heroType.calculateStat(Stat.AGI, newLvl), true)
        SetHeroInt(this.hero, this.heroType.calculateStat(Stat.INT, newLvl), true)

        wrapper.updateBaseStatsHero()

        wrapper.updateAll()
        wrapper.applyAll()

        this.level = newLvl

    private function onLevelUp()
        this.upgrade()

    private static function onLevelUpStatic()
        unitToHero.get( GetTriggerUnit()).onLevelUp()
    
    //handles assists
    private function addAssistDuration(int playerId)
        this.assistMap.put(playerId ,getElapsedGameTime() + assistDuration)
        this.lastAssistant = playerId

    private function onDamage()
        let damageSource = GetEventDamageSource().getOwner()
        let damageSourceId = damageSource.getId()
        if damageSource != null
            if team == TEAM_1
                if Teams.first2() <= damageSourceId and damageSourceId <= Teams.last2()
                    this.addAssistDuration(damageSource.getId())
            else
                if Teams.first1() <= damageSourceId and damageSourceId <= Teams.last1()
                    this.addAssistDuration(damageSource.getId())

    private static function onDamageStatic()
        unitToHero.get( GetTriggerUnit()).onDamage()

    private function revive()
        TimerDialogDisplay(this.reviveWindow, false)
        vec2 pos
        if this.team == TEAM_1
            pos = gg_rct_GreenAltar.getCenter()+vec2(128,128)
        else
            pos = gg_rct_BrownAltar.getCenter()-vec2(128,128)
            
        this.hero.revive(pos, true)
        this.hero.setMana(this.hero.getMaxMana())
        this.hero.setTimeScale(1)
        this.upgrade()
        if this.hero.getMoveSpeed() < 100
            let wrapper = this.hero.wrapper()
            Log.warn(this.hero.getName()+" has very low movement speed after revive.")
            Log.warn("getStatTotalValue = "+wrapper.getMoveSpeed().toString())
            Log.warn("getStatTotalBonus = "+wrapper.getMoveSpeedBonus().toString())
            Log.warn("getStatAbsoluteBonus = "+wrapper.getMoveSpeedAbsoluteBonus().toString())
            Log.warn("getStatRelativeBonus = "+wrapper.getMoveSpeedRelativeBonus().toString())
            Log.warn("getStatBaseValue = "+wrapper.getMoveSpeedBase().toString())
            Log.warn("actual ms = "+this.hero.getMoveSpeed().toString())
            Log.warn("default ms = "+this.hero.getDefaultMovespeed().toString() )

        if this.heroGlow != null
            this.heroGlow
                ..setXY(pos)
                ..setVertexColor(colorA(255, 255, 255, 255))

    private static function reviveStatic()
        let t = GetExpiredTimer()
        let hero = t.getData() castTo HeroImpl
        t.pause()
        hero.revive()

    //handles death
    private function onDeath()
        //indetify true killer
        if this.heroGlow != null
            this.heroGlow.setVertexColor(colorA(255, 255, 255, 0))

        player killingPlayer = GetKillingUnit().getOwner()
        int killingId = killingPlayer.getId()

        //replace killer with last assistant, if killer is not an enemy hero
        boolean replaceKiller = false

        //team 1 hero dies
        if this.team == TEAM_1
            //killer not in team 2
            if killingId < Teams.first2() or killingId > Teams.last2()
                replaceKiller = true
        //team 2 hero dies
        else
            //killer not in team 1
            if killingId < Teams.first1() or killingId > Teams.last1()
                replaceKiller = true

        let gameTime = getElapsedGameTime()
        let assistList = new LinkedList<int>
        int assistListId = 0
        string assistString
        boolean unknownKiller = false

        if replaceKiller
            if lastAssistant != -1 and assistMap.get(lastAssistant) > gameTime
                killingId = lastAssistant
                killingPlayer = players[lastAssistant]
            else
                if killingId != Teams.army1() and killingId != Teams.army2() and killingId != PLAYER_NEUTRAL_AGGRESSIVE
                    unknownKiller = true

        if this.team == TEAM_1
            for assistantId in Teams.HERO_PLAYERS[Teams.team(false)]
                let time = this.assistMap.get(assistantId)
                if time > gameTime and assistantId != killingId
                    assistList.add(assistantId)
                    assistListId += LIST_ID_POW_2[assistantId]
            assistString = assistStringListTeam2[assistListId]
        else
            for assistantId in Teams.HERO_PLAYERS[Teams.team(true)]
                let time = this.assistMap.get(assistantId)
                if time > gameTime and assistantId != killingId
                    assistList.add(assistantId)
                    assistListId += LIST_ID_POW_2[assistantId]
            assistString = assistStringListTeam1[assistListId]
        let assistants = assistList.size()

        if not unknownKiller
            if killingId <= Teams.last2()
                let assistBounty = ASSIST_BOUNTY[assistants]
                let killBounty = KILL_BOUNTY[assistants]
                if assistants == 0
                    printTimed(Players.coloredName(this.ownerId) + " was killed by " + Players.coloredName(killingId) + " for |cffffcc00"+killBounty.toString()+"|r gold.", 10)
                else
                    printTimed(Players.coloredName(this.ownerId) + " was killed by " + Players.coloredName(killingId) +
                    " for |cffffcc00" + killBounty.toString() + "|r gold. " + assistString + "|cffffcc00" + assistBounty.toString() +"|r gold for helping.", 10)
                doAfter(0.1) ->
                    for p in assistList
                        PlayerWrapper.addGoldEx(p, assistBounty)
                        PlayerWrapper.addAssist(p)
                    PlayerWrapper.addGoldEx(killingId, killBounty)
                    PlayerWrapper.addHeroKill(killingId, killingId <= Teams.last2())
                    let xp = (this.level + 5) * 10.
                    Teams.rewardXp(Teams.invert(this.team), this.hero.getPos(), xp)
                    destroy assistList
            else
                printTimed(Players.coloredName(this.ownerId) + " was killed by " + killingPlayer.getName() + ".", 10)
                destroy assistList
        else
            printTimed(Players.coloredName(this.ownerId) + " died.", 10)
            destroy assistList
        this.reviveTimer.start(10.+level*2, function reviveStatic)
        if GetLocalPlayer() == owner
            TimerDialogDisplay(this.reviveWindow, true)
        doAfter(0.1) ->
            PlayerWrapper.addDeath(this.ownerId)

    private static function onDeathStatic()
        unitToHero.get( GetTriggerUnit()).onDeath()

    override function toString() returns string
        return "Hero "+ this.owner.getName() +"="+this.hero.getName()

    override function getHeroType() returns IHeroType
        return this.heroType

    override function getLevel() returns int
        return this.level

    override function addXp(real xp)
        this.sharedXp += xp
        let intXp = this.sharedXp.toInt()
        this.hero.addXp(intXp, true)
        this.sharedXp -= intXp

    //once the game is over
    override function destroyTimer()
        TimerDialogDisplay(this.reviveWindow, false)
        DestroyTimerDialog(this.reviveWindow)
        this.reviveTimer.release()

    /*
    **  Changing cooldowns of morph abilities crashes the game, once they are used.
    */
    override function updateCooldowns()
        /*
        for i = 0 to 3
            real newCd = (this.defaultCooldown[i]+this.cooldownMod[i]) / (1.0+0.01*heroUnit.wrapper().getStatTotalValue(Stat.CD))
            BlzSetUnitAbilityCooldown(heroUnit, skillIds[i], GetUnitAbilityLevel(heroUnit, skillIds[i]), newCd )  */
        
        /*
            Tooltips do not need to be updated instantly. This serves as a cooldown to prevent lag spikes.
        */
        tooltipUpdateTimer.start(0.125) ->
            let hero = GetExpiredTimer().getData() castTo HeroImpl
            let heroType = hero.getHeroType()
            let heroUnit = hero.toUnit()
            heroType.updateTooltipsStr(heroUnit)
            heroType.updateTooltipsInt(heroUnit)

public function unit.toHero() returns IHero
    return HeroImpl.getHero(this)
        
public abstract class Hero
    static function create(int playerId, int unitId, vec2 pos, angle facing) returns IHero
        return new HeroImpl(playerId, unitId, pos, facing)

    static function setup(unit u) returns IHero
        return new HeroImpl(u)

    static function copyInventory(unit copyFrom, unit copyTo)
        for i = 0 to 5
            let itm = copyTo.itemInSlot(i)
            let itmId = copyFrom.itemInSlot(i).getTypeId()
            if itm.getTypeId() != itmId
                itm.remove()
                copyTo.addItemToSlot(itmId, i)

init
    HeroImpl.classInit()
    initAssistLists()
    CreateTrigger()
        ..addAction(function HeroImpl.regenerate)
        ..registerTimerEvent(0.1, true)
