package DynamicTooltips

import UtilityFunctions
import LinkedList
import UnitWrapper
import AbilityObjEditing
import HashMap
import HeroObjectEditing
import ObjectIds

/*  This flag can be set to false to disable setting tooltips at compiletime
**  Could speed up compiling the map. For the moment it is required due to a bug in 1.30 */
public constant COMPILETIME_TOOLTIPS_NORMAL = false
public constant COMPILETIME_TOOLTIPS_LEARN = false

public constant DYNAMIC_TOOLTIPS = true

public constant C_STR = color(255, 187, 102)
public constant C_INT = color(102, 187, 255)

abstract class DynamicElement
    function evaluate(int _strength, int _intelligence, real _cooldown, bool _const) returns string
        return ""

class DynamicText extends DynamicElement
    string message

    construct(string message)
        this.message = message

    override function evaluate(int strength, int intelligence, real cooldown, bool const) returns string
        return this.message

class DynamicStrength extends DynamicElement
    real factor
    construct(real factor)
        this.factor = factor

    override function evaluate(int strength, int intelligence, real cooldown, bool const) returns string
        if const
            return C_STR.toColorString()+" (+"+(factor).toSmartString()+"% str)|r "
        else
            return C_STR.toColorString()+" (+"+(strength * factor/100).toSmartString()+")|r "

class DynamicIntelligence extends DynamicElement
    real factor
    construct(real factor)
        this.factor = factor

    override function evaluate(int strength, int intelligence, real cooldown, bool const) returns string
        if const
            return C_INT.toColorString()+" (+"+(factor).toSmartString()+"% int)|r "
        else
            return C_INT.toColorString()+" (+"+(intelligence * factor/100).toSmartString()+")|r "

class DynamicCooldown extends DynamicElement
    construct()

    override function evaluate(int strength, int intelligence, real cooldown, bool const) returns string
        return cooldown.toSmartString()

public class DynamicTooltip
    
    private int abilityTypeId

    private string learnIntro
    private LinkedList<DynamicElement> array[5] normalList
    private LinkedList<DynamicElement> array[5] turnOffList
    private LinkedList<DynamicElement> array[5] learnLevelList
    private LinkedList<DynamicElement> learnList

    /*A short summary of the skill. Defaults to learnIntro. If learnIntro is too long, it is recommended to add
    a short summary, to keep tavern tooltips short.*/
    private string summary = ""

    private boolean useTurnOff

    private static constant idToTooltip = new HashMap<int,DynamicTooltip>

    private constant replaceIntelligence = new LinkedList<real>
    private constant replaceStrength = new LinkedList<real>
    private AbilityDefinition aDef = null

    function getAbilityTypeId() returns int
        return this.abilityTypeId

    static function getDynamicTooltip(int abilityTypeId) returns DynamicTooltip
        return idToTooltip.get(abilityTypeId)

    construct(int abilityTypeId)
        this.abilityTypeId = abilityTypeId
        this.useTurnOff = false
        idToTooltip.put(abilityTypeId, this)
        if compiletime
            this.aDef = ABIL_DEF_MAP.get(this.abilityTypeId)


    private function evaluateList(LinkedList<DynamicElement> list, int strength, int intelligence, real cooldown, bool const) returns string
        string s = ""
        for elem in list
            s+=elem.evaluate(strength, intelligence, cooldown, const)
        return s

    /*
    **  Dynamic Tooltips are list based, because iterating through a list is a lot faster compared to replacing substrings.
    */
    private function convertToList(string tooltip) returns LinkedList<DynamicElement>
        let list = new LinkedList<DynamicElement>()
        int current = 0
        int i = 0
        let l = tooltip.length()
        while i<l
            let substr = tooltip.charAt(i)
            if(substr=="{")
                list.add(new DynamicText(tooltip.substring(current, i)))
                let referenceType = tooltip.charAt(i+1)
                if(referenceType=="I")
                    let id = S2I(tooltip.charAt(i+2))
                    if replaceIntelligence.size() <= id
                        if compiletime
                            compileError("Replace int list missing entry: "+I2S(id)+ " for tooltip:\""+tooltip+"\"")
                    list.add(new DynamicIntelligence(replaceIntelligence.get(id)))
                else if(referenceType=="S")
                    let id = S2I(tooltip.charAt(i+2))
                    if replaceStrength.size() <= id
                        if compiletime
                            compileError("Replace str list missing entry: "+I2S(id)+ " for tooltip:\""+tooltip+"\"")
                    list.add(new DynamicStrength(replaceStrength.get(id)))
                else if(referenceType=="C")
                    list.add(new DynamicCooldown())
                i+=3
                current = i+1
            i++

        list.add(new DynamicText(tooltip.substring(current)))
        return list

    function getUseTurnOff() returns boolean
        return this.useTurnOff

    function writeData()
        if aDef == null
            return
        if COMPILETIME_TOOLTIPS_LEARN
            aDef.setTooltipLearnExtended(this.evaluateLearnString())

        if COMPILETIME_TOOLTIPS_NORMAL
            for i = 1 to 5
                aDef.setTooltipNormalExtended(i, this.evaluateNormalString(i))
            if this.getUseTurnOff()
                for i = 1 to 5
                    aDef.setTooltipTurnOffExtended(i, this.evaluateTurnOffString(i))

    function setReplaceIntelligence(vararg real replacements)
        for replacement in replacements
            replaceIntelligence.add(replacement)

    function setReplaceStrength(vararg real replacements)
        for replacement in replacements
            replaceStrength.add(replacement)
        

    function setSummary(string summary)
        this.summary = summary

    function setLearnString(string intro, LevelString levelString)
        string newString = intro + "\n"
        this.learnIntro = intro
        for i = 1 to 5
            newString += getLevelString(i) + levelString.getString(i)
            this.learnLevelList[i-1] = this.convertToList(levelString.getString(i))

        destroy levelString
        newString += "\n "
        this.learnList = this.convertToList(newString)


    function setNormalString(boolean showCooldown, int level, string normalString)
        string newString = normalString

        if showCooldown
            if compiletime
                newString += "\n" + "Cooldown: " + "<" + this.getAbilityTypeId().toRawCode() + ",Cool"+level.toString()+">" + " seconds\n "
            else
                newString += "\n" + "Cooldown: " + "{CD}" + " seconds\n "
        else
            newString += "\n "

        this.normalList[level - 1] = this.convertToList(newString)


    function setTurnOffString(boolean showCooldown, int level, string turnOffString)
        this.useTurnOff = true
        string newString = turnOffString

        if showCooldown
            if compiletime
                newString += "\n" + "Cooldown: " + "<" + this.getAbilityTypeId().toRawCode() + ",Cool"+level.toString()+">" + " seconds\n "
            else
                newString += "\n" + "Cooldown: " + "{CD}" + " seconds\n "
        else
            newString += "\n "
            
        this.turnOffList[level - 1] = this.convertToList(newString)


    function getLearnIntro() returns string
        return this.learnIntro

    function getSummary() returns string
        if this.summary == ""
            return this.getLearnIntro()
        else
            return this.summary
    
    function evaluateLearnStringLevel(int level, int strength, int intelligence, bool const) returns string
        return this.evaluateList(this.learnLevelList[level-1], strength, intelligence, -1, const)

    function evaluateLearnString(int strength, int intelligence, bool const) returns string
        return this.evaluateList(this.learnList, strength, intelligence, -1, const)

    function evaluateNormalString(int level, int strength, int intelligence, real cooldown, bool const) returns string
        string ret = ""
        if level != 0
            ret = this.evaluateList(this.normalList[level-1], strength, intelligence, cooldown, const)
        return ret

    function evaluateTurnOffString(int level, int strength, int intelligence, real cooldown, bool const) returns string
        string ret = ""
        if level != 0
            ret = this.evaluateList(this.turnOffList[level-1], strength, intelligence, cooldown, const)
        return ret

    // Overloaded functions:

    function evaluateLearnStringLevel(int level) returns string
        return evaluateLearnStringLevel(level, 100, 100, true)

    function evaluateLearnStringLevel(unit whichUnit) returns string
        return evaluateLearnStringLevel(whichUnit.getAbilityLevel(this.abilityTypeId), whichUnit.getStr(), whichUnit.getInt(), false)

    function evaluateLearnStringLevel(unit whichUnit, int level) returns string
        return evaluateLearnStringLevel(level, whichUnit.getStr(), whichUnit.getInt(), false)


    function evaluateLearnString() returns string
        return evaluateLearnString(100, 100, true)

    function evaluateLearnString(unit whichUnit) returns string
        return evaluateLearnString(whichUnit.getStr(), whichUnit.getInt(), false)


    function evaluateNormalString(int level) returns string
        return evaluateNormalString(level, 100, 100, -1 ,true)
    
    function evaluateNormalString(unit whichUnit) returns string
        let level = whichUnit.getAbilityLevel(this.abilityTypeId)
        return evaluateNormalString(level, whichUnit.getStr(), whichUnit.getInt(), BlzGetUnitAbilityCooldown(whichUnit, this.abilityTypeId, level - 1), false)

    function evaluateNormalString(unit whichUnit, int level) returns string
        return evaluateNormalString(level, whichUnit.getStr(), whichUnit.getInt(), BlzGetUnitAbilityCooldown(whichUnit, this.abilityTypeId, level - 1), false)

    function evaluateTurnOffString(int level) returns string
        return evaluateTurnOffString(level, 100, 100, -1, true)

    function evaluateTurnOffString(unit whichUnit) returns string
        let level = whichUnit.getAbilityLevel(this.abilityTypeId)
        return evaluateTurnOffString(level, whichUnit.getStr(), whichUnit.getInt(), BlzGetUnitAbilityCooldown(whichUnit, this.abilityTypeId, level - 1), false)

    function evaluateTurnOffString(unit whichUnit, int level) returns string
        return evaluateTurnOffString(level, whichUnit.getStr(), whichUnit.getInt(), BlzGetUnitAbilityCooldown(whichUnit, this.abilityTypeId, level - 1), false)