package ItemsDamageMagical

import ItemConfiguration

constant CATEGORY = 4

function itemBootsOfDarkness()
    let ms = 40
    let voidDmg = 15
    let intelFactor = 12
    setupItem(ITEM_ID_BOOTS_OF_DARKNESS, CATEGORY, 2400)
    setInfo("Boots Of Darkness", "+ {0} movement speed|n+ {1}% intelligence|nAttacks deal {2} additional void damage.|nMovement speed bonus from boots does not stack."
    .format(ms.toString(), intelFactor.toString(), voidDmg.toString()))

    addRequirement(ITEM_ID_LIGHT_BOOTS)
    addRequirement(ITEM_ID_GEM_OF_DARKNESS)

    addStatBonusRelative(Stat.INT, intelFactor*10)
    addPickupFunction(()->(begin
        if isUnique() and hasNot(ITEM_ID_HEAVY_BOOTS) and hasNot(ITEM_ID_LIGHT_BOOTS)
            getTriggerUnit().wrapper().addStatAbsoluteBonus(Stat.MS, ms)
    end))

    addDropFunction(()->(begin
        if isUnique() and hasNot(ITEM_ID_HEAVY_BOOTS) and hasNot(ITEM_ID_LIGHT_BOOTS)
            getTriggerUnit().wrapper().addStatAbsoluteBonus(Stat.MS, -ms)
    end))

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            source.dealDamage(target, voidDmg.toReal() * source.countItemsOfType(ITEM_ID_BOOTS_OF_DARKNESS) , ReductionType.NONE)
            target.addEffect(Abilities.avengerMissile, "chest").destr()



function itemMageAmulet()
    let mp = 100
    let hp = 100
    setupItem(ITEM_ID_MAGE_AMULET, CATEGORY, 825)
    setInfo("Mage Amulet", "+ {0} mana points|n +{1} hit points".format(mp.toString(), hp.toString()))
    addStatBonusAbsolute(Stat.MP, mp)
    addStatBonusAbsolute(Stat.HP, hp)



function itemManaRing()
    let mr = 10
    
    setupItem(ITEM_ID_MANA_RING, CATEGORY, 500)
    setInfo("Ring of Mana", "+ {0} mana point regeneration per 10 seconds".format(mr.toString()))

    addStatBonusAbsolute(Stat.MR, mr)



function itemWizardsHat()
    let intel = 8
    
    setupItem(ITEM_ID_WIZARDS_HAT, CATEGORY, 400)
    setInfo("Wizard's Hat", "+ {0} intelligence".format(intel.toString()))
    
    addStatBonusAbsolute(Stat.INT, intel)

            
function itemGemOfMana()
    let mp = 100
    
    setupItem(ITEM_ID_GEM_OF_MANA, CATEGORY, 500)
    setInfo("Gem of Mana", "+ {0} mana points".format(mp.toString()))
    
    addStatBonusAbsolute(Stat.MP, mp)
            
function itemGemOfDarkness()
    let voidDmg = 12
    
    setupItem(ITEM_ID_GEM_OF_DARKNESS, CATEGORY, 500)
    setInfo("Gem of Darkness", "Attacks deal {0} additional void damage.".format(voidDmg.toString()))
    
    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            source.dealDamage(target, voidDmg.toReal() * source.countItemsOfType(ITEM_ID_GEM_OF_DARKNESS) , ReductionType.NONE)
            target.addEffect(Abilities.avengerMissile, "chest").destr()


function itemSkull()
    let mana = 8 // trigger
    
    setupItem(ITEM_ID_SKULL, CATEGORY, 550)
    setInfo("Skull", "Restores {0} mana points per kill.".format(mana.toString()))
    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(() -> (begin
            let killer = GetKillingUnit()
            if not GetTriggerUnit().isType(UNIT_TYPE_STRUCTURE) and killer.hasItemById(ITEM_ID_SKULL)
                killer.healMana(killer, 8. * killer.countItemsOfType(ITEM_ID_SKULL))
                killer.addEffect(Abilities.spiritTouchTarget, "origin").destr()
        end))

function itemVoodooDoll()
    let mp = 5
    
    setupItem(ITEM_ID_VOODOO_DOLL, CATEGORY, 600)
    setInfo("Voodoo Doll", "Restores {0} mana points everytime you are attacked.".format(mp.toString()))

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            target.healMana(target, mp.toReal() * target.countItemsOfType(ITEM_ID_VOODOO_DOLL))
            target.addEffect(Abilities.spiritTouchTarget, "origin").destr()

function itemSpellBook()
    let intel = 16
    
    setupItem(ITEM_ID_SPELL_BOOK, CATEGORY, 800)
    setInfo("Spell Book", "+ {0} intelligence".format(intel.toString()))
    
    addStatBonusAbsolute(Stat.INT, intel)

function itemMagicStaff()
    let mana = 7
    
    setupItem(ITEM_ID_MAGIC_STAFF, CATEGORY, 640)
    setInfo("Magic Staff", "Attacks drain {0} mana from the target.".format(mana.toString()))
    
    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            if target.getMana() > 0
                let manaDrained = min(mana.toReal() * source.countItemsOfType(ITEM_ID_MAGIC_STAFF) , target.getMana())
                source.healMana(source, manaDrained)
                source.damageMana(target, manaDrained, ReductionType.NONE)
                target.addEffect(Abilities.arcaneTowerAttack, "chest").destr()


function itemApprenticeScroll()
    let factor = 35 //trigger
    
    setupItem(ITEM_ID_APPRENTICES_SCROLL, CATEGORY, 500)
    setInfo("Apprentice's Scroll", "Restores {0}% of your mana, when you level up. This effect does not stack.".format(factor.toString()))

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_HERO_LEVEL)
        ..addAction(() -> (begin
            let hero = GetTriggerUnit()
            if hero.hasItemById(ITEM_ID_APPRENTICES_SCROLL)
                hero.healMana(hero, hero.getMaxMana()*0.35)
                hero.addEffect(Abilities.aImaTarget, "chest").destr()
        end))


function itemSkullPendant()
    let hp = 125
    let mp = 150
    let killMana = 15 //trigger
    setupItem(ITEM_ID_SKULL_PENDANT, CATEGORY, 2100)
    setInfo("Skull Pendant", "+ {0} mana points|n +{1} hit points|nRestores {2} mana points per kill.".format(
            mp.toString(), hp.toString(), killMana.toString()
        ))
    addRequirement(ITEM_ID_SKULL)
    addRequirement(ITEM_ID_MAGE_AMULET)
    
    addStatBonusAbsolute(Stat.HP, hp)
    addStatBonusAbsolute(Stat.MP, mp)

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(() -> (begin
            let killer = GetKillingUnit()
            if not GetTriggerUnit().isType(UNIT_TYPE_STRUCTURE) and killer.hasItemById(ITEM_ID_SKULL_PENDANT)
                killer.healMana(killer, 15. * killer.countItemsOfType(ITEM_ID_SKULL_PENDANT))
                killer.addEffect(Abilities.spiritTouchTarget, "origin").destr()
        end))


function itemVisionWard()
    let mp = 225
    let hp = 225
    let intel = 25
    let mana = 10
    setupItem(ITEM_ID_VISION_WARD, CATEGORY, 3450)
    setInfo("Vision Ward", "+ {0} mana points|n+ {1} hit points|n+ {2} intelligence|nAttacks drain {3} mana from the target.|nActive:|nSummons an invulnerable and immovable ward that provides vision in an area.".format(
            mp.toString(), hp.toString(), intel.toString(), mana.toString()
        ))
    addRequirement(ITEM_ID_MAGIC_STAFF)
    addRequirement(ITEM_ID_GEM_OF_MANA)
    addRequirement(ITEM_ID_GEM_OF_HEALTH)
    setAbilities('A00D')
    
    addStatBonusAbsolute(Stat.MP, mp)
    addStatBonusAbsolute(Stat.HP, hp)
    addStatBonusAbsolute(Stat.INT, intel)

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            if target.getMana() > 0
                let manaDrained = min(mana.toReal() * source.countItemsOfType(ITEM_ID_VISION_WARD) , target.getMana())
                source.healMana(source, manaDrained)
                source.damageMana(target, manaDrained, ReductionType.NONE)
                target.addEffect(Abilities.arcaneTowerAttack, "chest").destr()



            
function itemVoodooMask()
    let mp = 10
    let voidDmg = 15

    setupItem(ITEM_ID_VOODOO_MASK, CATEGORY, 1750)
    setInfo("Voodoo Mask", "Attacks deal {0} additional void damage.|nRestores {1} mana points everytime you are attacked.".format(
            voidDmg.toString(), mp.toString()
        ))
    addRequirement(ITEM_ID_VOODOO_DOLL)
    addRequirement(ITEM_ID_GEM_OF_DARKNESS)

    if not compiletime
        addHitFunction() (unit source, unit target, real damage) ->
            target.healMana(target, mp.toReal() * target.countItemsOfType(ITEM_ID_VOODOO_MASK))
            target.addEffect(Abilities.spiritTouchTarget, "origin").destr()
        addDamageFunction() (unit source, unit target, real damage) ->
            if source.hasItemById(ITEM_ID_VOODOO_MASK) and not target.isType(UNIT_TYPE_STRUCTURE)
                source.dealDamage(target, voidDmg.toReal() * source.countItemsOfType(ITEM_ID_VOODOO_MASK) , ReductionType.NONE)
                target.addEffect(Abilities.avengerMissile, "chest").destr()


function itemEvilBook()
    let intel = 30
    let factor = 50 //trigger
    
    setupItem(ITEM_ID_EVIL_BOOK, CATEGORY, 1700)
    setInfo("Evil Book", "+ {0} intelligence|nRestores {1}% of your mana, when you level up. This effect does not stack.".format(
            intel.toString(), factor.toString()
        ))
    addRequirement(ITEM_ID_APPRENTICES_SCROLL)
    addRequirement(ITEM_ID_SPELL_BOOK)
    addStatBonusAbsolute(Stat.INT, intel)

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_HERO_LEVEL)
        ..addAction(() -> (begin
            let hero = GetTriggerUnit()
            if hero.hasItemById(ITEM_ID_EVIL_BOOK)
                hero.healMana(hero, hero.getMaxMana()*0.5)
                hero.addEffect(Abilities.aImaTarget, "chest").destr()
        end))
            
function itemFrostRing()
    let mp = 200
    let mr = 20
    
    setupItem(ITEM_ID_RING_OF_REGEN, CATEGORY, 1900)
    setInfo("Ring of Regeneration", "+ {0} mana points|n+ {1} mana point regeneration per 10 seconds".format(
            mp.toString(), mr.toString()
        ))
    addRequirement(ITEM_ID_GEM_OF_MANA)
    addRequirement(ITEM_ID_MANA_RING)
    
    addStatBonusAbsolute(Stat.MP, mp)
    addStatBonusAbsolute(Stat.MR, mr)

function itemScepterOfTheTides()
    let intel = 25
    let voidDmg = 15
    let manaDrain = 10
    
    setupItem(ITEM_ID_SCEPTER_OF_THE_TIDES, CATEGORY, 2050)
    setInfo("Scepter of the Tides", "+ {0} intelligence|nDrains {1} mana and deals {2} void damage per attack.".format(
            intel.toString(), manaDrain.toString(), voidDmg.toString()
        ))
    addRequirement(ITEM_ID_MAGIC_STAFF)
    addRequirement(ITEM_ID_GEM_OF_DARKNESS)
    
    addStatBonusAbsolute(Stat.INT, intel)

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            let manaDrained = min(manaDrain.toReal() * source.countItemsOfType(ITEM_ID_SCEPTER_OF_THE_TIDES) , target.getMana())
            source.dealDamage(target, voidDmg.toReal() * source.countItemsOfType(ITEM_ID_SCEPTER_OF_THE_TIDES) , ReductionType.NONE)
            source.healMana(source, manaDrained)
            source.damageMana(target, manaDrained, ReductionType.NONE)
            target.addEffect(Abilities.avengerMissile, "chest").destr()
            target.addEffect(Abilities.arcaneTowerAttack, "chest").destr()

function itemMagmaCrystal()
    let mp = 125
    let hp = 190
    let magicDmg = 20
    
    setupItem(ITEM_ID_MAGMA_CRYSTAL, CATEGORY, 1750)
    setInfo("Magma Crystal", "+ {0} hit points|n+ {1} mana points|nAttacks deal {2} additional magic damage.".format(
            hp.toString(), mp.toString(), magicDmg.toString()
        ))

    addRequirement(ITEM_ID_GEM_OF_DARKNESS)
    addRequirement(ITEM_ID_GEM_OF_HEALTH)
    addRequirement(ITEM_ID_GEM_OF_MANA)
    
    addStatBonusAbsolute(Stat.MP, mp)
    addStatBonusAbsolute(Stat.HP, hp)

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            source.dealDamage(target, magicDmg.toReal() * source.countItemsOfType(ITEM_ID_MAGMA_CRYSTAL) , ReductionType.MAGIC)
            target.addEffect(Abilities.fireBallMissile, "chest").destr()

function itemCursedMask()
    let intel = 25
    let killMana = 12 //trigger
    
    setupItem(ITEM_ID_CURSED_MASK, CATEGORY, 2000)
    setInfo("Cursed Mask", "+ {0} intelligence|nRestores {1} mana points per kill.".format(
            intel.toString(), killMana.toString()
        ))

    addRequirement(ITEM_ID_SKULL)
    addRequirement(ITEM_ID_WIZARDS_HAT)
    
    addStatBonusAbsolute(Stat.INT, intel)

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(() -> (begin
            let killer = GetKillingUnit()
            if not GetTriggerUnit().isType(UNIT_TYPE_STRUCTURE) and killer.hasItemById(ITEM_ID_CURSED_MASK)
                killer.healMana(killer, 12. * killer.countItemsOfType(ITEM_ID_CURSED_MASK))
                killer.addEffect(Abilities.spiritTouchTarget, "origin").destr()
        end))

function itemEmeraldPendant()
    let hp = 275
    let mp = 275
    let killMana = 25 //trigger
    
    setupItem(ITEM_ID_EMERALD_PENDANT, CATEGORY, 3500)
    setInfo("Emeral Pendant", "+ {0} mana points|n +{1} hit points|nRestores {2} mana points per kill.".format(
            mp.toString(), hp.toString(), killMana.toString()
        ))

    addRequirement(ITEM_ID_SKULL_PENDANT)
    addRequirement(ITEM_ID_GEM_OF_HEALTH)
    addRequirement(ITEM_ID_GEM_OF_MANA)
    
    addStatBonusAbsolute(Stat.HP, hp)
    addStatBonusAbsolute(Stat.MP, mp)

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(() -> (begin
            let killer = GetKillingUnit()
            if not GetTriggerUnit().isType(UNIT_TYPE_STRUCTURE) and killer.hasItemById(ITEM_ID_EMERALD_PENDANT)
                killer.healMana(killer, 25. * killer.countItemsOfType(ITEM_ID_EMERALD_PENDANT))
                killer.addEffect(Abilities.spiritTouchTarget, "origin").destr()
        end))
            
    

function itemRingOfDarkness()
    let intel = 25
    let mr = 15

    let mp = 12
    let voidDmg = 16

    let voidExplosion = 30 //trigger
    
    setupItem(ITEM_ID_RING_OF_DARKNESS, CATEGORY, 3700)
    setInfo("Ring of Darkness", "+ {0} intelligence|n+ {1} mana point regeneration per 10 seconds|nAttacks deal {2} additional void damage.|nRestores {3} mana points everytime you are attacked.|nKilling a unit creates a void explosion, dealing {4} damage to neaby enemies.".format(
            intel.toString(), mr.toString(), voidDmg.toString(), mp.toString(), voidExplosion.toString()
        ))

    addRequirement(ITEM_ID_MANA_RING)
    addRequirement(ITEM_ID_VOODOO_MASK)
    
    addStatBonusAbsolute(Stat.INT, intel)
    addStatBonusAbsolute(Stat.MR, mr)

    if not compiletime
        addHitFunction() (unit source, unit target, real damage) ->
            target.healMana(target, mp.toReal() * target.countItemsOfType(ITEM_ID_RING_OF_DARKNESS))
            target.addEffect(Abilities.spiritTouchTarget, "origin").destr()
    
        addDamageFunction() (unit source, unit target, real damage) ->
            if source.hasItemById(ITEM_ID_RING_OF_DARKNESS) and not target.isType(UNIT_TYPE_STRUCTURE)
                source.dealDamage(target, voidDmg.toReal() * source.countItemsOfType(ITEM_ID_RING_OF_DARKNESS) , ReductionType.NONE)
                target.addEffect(Abilities.avengerMissile, "chest").destr()

        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(() -> (begin
            let killer = GetKillingUnit()
            let dyingUnit = GetTriggerUnit()
            if not dyingUnit.isType(UNIT_TYPE_STRUCTURE) and killer.hasItemById(ITEM_ID_RING_OF_DARKNESS)
                addEffect(Abilities.annihilationMissile, dyingUnit.getPos()).destr()
                let g = CreateGroup()
                g.enumUnitsInRange(dyingUnit.getPos(), 250)
                for unit u in g
                    if not u.isType(UNIT_TYPE_STRUCTURE) and u.isTargetable(killer.getOwner(), false, true)
                        killer.dealDamage(u, 30, ReductionType.NONE)
                g.destr()
        end))
            

group blessedHeroes = CreateGroup()

function blessedRegeneration()
    let blessedHero = GetEnumUnit()
    let missingMana = blessedHero.getMaxMana() - blessedHero.getMana()
    blessedHero.healMana(blessedHero, missingMana * 0.01 * 0.25)

function itemBlessedGoblet()
    let mp = 300
    let hp = 150
    let mr = 1 //trigger ^
    
    setupItem(ITEM_ID_BLESSED_GOBLET, CATEGORY, 4000)
    setInfo("Blessed Goblet", "+ {0} hit points|n+ {1} mana points|nRegenerates {2}% of your missing mana per second.".format(
            hp.toString(), mp.toString(), mr.toString()
        ))

    addRequirement(ITEM_ID_RING_OF_REGEN)
    addRequirement(ITEM_ID_GEM_OF_HEALTH)
    addRequirement(ITEM_ID_GEM_OF_MANA)
    
    addStatBonusAbsolute(Stat.MP, mp)
    addStatBonusAbsolute(Stat.HP, hp)

    
    addPickupFunction(() -> (begin
        if isUnique()
            blessedHeroes.addUnit(getTriggerUnit())
    end))
    addDropFunction(() -> (begin
        if isUnique()
            blessedHeroes.removeUnit(getTriggerUnit())
    end))

    if not compiletime
        createItemEffectTrigger()
        ..registerTimerEvent(0.25, true)
        ..addAction(() -> (begin
            ForGroup(blessedHeroes, function blessedRegeneration)
        end) )
    
            

function itemWandOfDeath()
    let intel = 35

    let voidDmg = 20
    let manaDrain = 10

    let dmgFactor = 2 // -> onAttack
    let duration = 10 // -> onAttack
    
    setupItem(ITEM_ID_WAND_OF_DEATH, CATEGORY, 3900)
    setInfo("Wand of Death", "+ {0} intelligence|nDrains {1} mana and deals {2} void damage per attack.|nDealing damage, reduces the target's willpower by {3}% of the damage dealt for {4} seconds.|nThis effect does not stack.".format(
            intel.toString(), manaDrain.toString(), voidDmg.toString(), dmgFactor.toString(), duration.toString()
        ))
    addRequirement(ITEM_ID_SCEPTER_OF_THE_TIDES)
    addRequirement(ITEM_ID_MAGIC_STAFF)
    addRequirement(ITEM_ID_GEM_OF_DARKNESS)
    
    addStatBonusAbsolute(Stat.INT, intel)

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            let manaDrained = min(manaDrain.toReal() * source.countItemsOfType(ITEM_ID_WAND_OF_DEATH) , target.getMana())
            source.dealDamage(target, voidDmg.toReal() * source.countItemsOfType(ITEM_ID_WAND_OF_DEATH) , ReductionType.NONE)
            source.healMana(source, manaDrained)
            source.damageMana(target, manaDrained, ReductionType.NONE)
            target.addEffect(Abilities.avengerMissile, "chest").destr()
            target.addEffect(Abilities.arcaneTowerAttack, "chest").destr()


            

function itemWandOfBlood()
    let intel = 30
    let hr = 35
    let hp = 200
    let lifesteal = 8 // -> onAttack
    
    setupItem(ITEM_ID_WAND_OF_BLOOD, CATEGORY, 3650)
    setInfo("Wand of Blood", "+ {0} intelligence|n+ {1} hit points|n+ {2} hit point regeneration per 10 seconds|nDealing damage restores hit points equal to {3}% of the damage dealt.|nThis effect does not stack.".format(
            intel.toString(), hp.toString(), hr.toString(), lifesteal.toString()
        ))
    addRequirement(ITEM_ID_MAGIC_STAFF)
    addRequirement(ITEM_ID_RING_OF_HEALTH)
    addRequirement(ITEM_ID_GEM_OF_HEALTH)
    
    addStatBonusAbsolute(Stat.INT, intel)
    addStatBonusAbsolute(Stat.HR, hr)
    addStatBonusAbsolute(Stat.HP, hp)

            
function itemArcaneBlade()
    let as = 20
    let mp = 300
    let dmg = 20
    let manaFactor = 5
    
    setupItem(ITEM_ID_ARCANE_BLADE, CATEGORY, 3800)
    setInfo("Arcane Blade", "+ {0} mana points|n+ {1}% attack speed|nDeals {2} + {3}% of your current mana as magic damage.|nThis effect does not stack.".format(
            mp.toString(), as.toString(), dmg.toString(), manaFactor.toString()
        ))
    addRequirement(ITEM_ID_RAVEN_SWORD)
    addRequirement(ITEM_ID_GEM_OF_MANA)
    
    addStatBonusAbsolute(Stat.AS, as)
    addStatBonusAbsolute(Stat.MP, mp)

    if not compiletime
        addDamageFunction() (unit source, unit target, real damage) ->
            source.dealDamage(target, (dmg + source.getMana() * manaFactor/100) , ReductionType.MAGIC)
            target.addEffect(Abilities.farseerMissile, "chest").destr()

function itemVampiricGoblet()
    let intel = 40
    let hp = 150
    let mp = 150
    let hpFactor = 20 // -> onAttack
    let manaFactor = 15 // -> onAttack

    let killMana = 30 //trigger
    
    setupItem(ITEM_ID_VAMPIRIC_GOBLET, CATEGORY, 4750)
    setInfo("Vampiric Goblet", "+ {0} mana points|n +{1} hit point|n+ {2} intelligence|nRestores {3} mana points per kill.|nDealing damage against heroes restores {4}% of the damage dealt as hitpoints and {5}% as mana points.|nThis effect does not stack.".format(
            mp.toString(), hp.toString(), intel.toString(), killMana.toString(), hpFactor.toString(), manaFactor.toString()
        ))
    addRequirement(ITEM_ID_SKULL_PENDANT)
    addRequirement(ITEM_ID_CURSED_MASK)
    
    addStatBonusAbsolute(Stat.INT, intel)
    addStatBonusAbsolute(Stat.HP, hp)
    addStatBonusAbsolute(Stat.MP, mp)

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addAction(() -> (begin
            let killer = GetKillingUnit()
            if not GetTriggerUnit().isType(UNIT_TYPE_STRUCTURE) and killer.hasItemById(ITEM_ID_VAMPIRIC_GOBLET)
                killer.healMana(killer, 30. * killer.countItemsOfType(ITEM_ID_VAMPIRIC_GOBLET))
                killer.addEffect(Abilities.spiritTouchTarget, "origin").destr()
        end))


function itemScrollOfConjuration()
    let intel = 40
    let intelFactor = 20
    
    setupItem(ITEM_ID_SCROLL_OF_CONJURATION, CATEGORY, 4300)
    setInfo("Scroll of Conjuration", "+ {0} intelligence|nIncreases your intelligence by {1}%.|nThis effect does not stack.|nRestores all of your mana, when you level up.".format(
            intel.toString(), intelFactor.toString()
        ))
    addRequirement(ITEM_ID_APPRENTICES_SCROLL)
    addRequirement(ITEM_ID_EVIL_BOOK)
    addRequirement(ITEM_ID_SPELL_BOOK)
    
    addStatBonusAbsolute(Stat.INT, intel)

    addPickupFunction(() -> (begin
        if isUnique()
            getTriggerUnit().wrapper().addStatRelativeBonus(Stat.INT,  intelFactor*10)
    end))
    addDropFunction(() -> (begin
        if isUnique()
            getTriggerUnit().wrapper().addStatRelativeBonus(Stat.INT,  -intelFactor*10)
    end))

    if not compiletime
        createItemEffectTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_HERO_LEVEL)
        ..addAction(() -> (begin
            let hero = GetTriggerUnit()
            if hero.hasItemById(ITEM_ID_SCROLL_OF_CONJURATION)
                hero.healMana(hero, hero.getMaxMana())
                hero.addEffect(Abilities.aImaTarget, "chest").destr()
        end))

function itemBracerOfPower()
    let hp = 200
    let mp = 200
    let hr = 30
    let mr = 30
    let bonus = 15 //trigger -> General
    
    setupItem(ITEM_ID_BRACER_OF_POWER, CATEGORY, 3700)
    setInfo("Bracer of Power", "+ {0} hit points|n+ {1} mana points|n+ {2} hit point regeneration per 10 seconds|n+ {3} mana point regeneration per 10 seconds|nIncreases all incoming effects that restore hit points or mana points by {4}%.".format(
            hp.toString(), mp.toString(), hr.toString(), mr.toString(), bonus.toString()
        ))
    addRequirement(ITEM_ID_RING_OF_REGEN)
    addRequirement(ITEM_ID_GEM_OF_HEALTH)
    addRequirement(ITEM_ID_RING_OF_HEALTH)
    
    addStatBonusAbsolute(Stat.HP, hp)
    addStatBonusAbsolute(Stat.MP, mp)
    addStatBonusAbsolute(Stat.HR, hr)
    addStatBonusAbsolute(Stat.MR, mr)


@compiletime
function initItems1()
    itemMageAmulet()
    itemManaRing()
    itemWizardsHat()
    itemGemOfMana()
    itemGemOfDarkness()
    itemSkull()
    itemVoodooDoll()
    itemSpellBook()
    itemMagicStaff()
    itemApprenticeScroll()


@compiletime
function initItems2()
    itemSkullPendant()
    itemVisionWard()
    itemVoodooMask()
    itemEvilBook()
    itemFrostRing()
    itemScepterOfTheTides()
    itemBootsOfDarkness()
    itemMagmaCrystal()
    itemCursedMask()

@compiletime
function initItems3()
    itemEmeraldPendant()
    itemRingOfDarkness()
    itemBlessedGoblet()
    itemWandOfBlood()
    itemWandOfDeath()
    itemArcaneBlade()
    itemVampiricGoblet()
    itemScrollOfConjuration()
    itemBracerOfPower()


init
    execute() ->
        initItems1()
    execute() ->
        initItems2()
    execute() ->
        initItems3()