package HeroControl

import PlayerControl
import HashMap
import GameTimer
import LinkedList

function integer.getHero() returns Hero
    return Hero.heroes[this]

class Hero

    static constant assistDuration = 10.

    HeroPlayer owner
    unit hero
    private static trigger onDamageTrigger = CreateTrigger()
    private static trigger onDeathTrigger = CreateTrigger()
    protected static Hero array heroes
    private static HashMap<unit,Hero> unitToHero = new HashMap<unit,Hero>
    HashMap<HeroPlayer,real> assistMap = new HashMap<HeroPlayer,real>

    

    static function classInit()
        onDamageTrigger.addAction(function onDamageStatic)
        onDeathTrigger.addAction(function onDeathStatic)

    construct(HeroPlayer p, int unitId, vec2 pos, angle facing )
        this.hero = createUnit(p.getPlayer(), unitId, pos, facing)
        this.owner = p
        unitToHero.put(this.hero, this)
        onDamageTrigger.registerUnitEvent(this.hero, EVENT_UNIT_DAMAGED)
        onDeathTrigger.registerUnitEvent(this.hero, EVENT_UNIT_DEATH)
    
    construct(unit u)
        this.hero = u
        this.owner = u.getOwner().getCPlayer() castTo HeroPlayer
        unitToHero.put(this.hero, this)
        onDamageTrigger.registerUnitEvent(this.hero, EVENT_UNIT_DAMAGED)
        onDeathTrigger.registerUnitEvent(this.hero, EVENT_UNIT_DEATH)

    //handles assists
    function addAssistDuration(HeroPlayer p)
        assistMap.put(p,getElapsedGameTime() + assistDuration)

    function onDamage()
        CPlayer damageSource
        damageSource = GetEventDamageSource().getOwner().getCPlayer()
        if( damageSource instanceof HeroPlayer and damageSource.getTeam()!=this.owner.getTeam())
            this.addAssistDuration(damageSource castTo HeroPlayer)

    static function onDamageStatic()
        unitToHero.get( GetTriggerUnit()).onDamage()

    //handles death
    function onDeath()
        LLIterator<HeroPlayer> iter = HeroPlayer.heroPlayers.iterator()
        while(iter.hasNext())
            HeroPlayer assistant = iter.next()
            if(assistMap.get(assistant) > getElapsedGameTime() )
                print("assist:"+assistant.toString())


    static function onDeathStatic()
        unitToHero.get( GetTriggerUnit()).onDeath()

    //kills the Hero without specifying the killer
    function killHero()
        this.hero.kill()
    
    //kills the Hero, the killer is specified
    function killHero(unit killer)
        this.hero.setHP(1)
        killer.damageTarget(this.hero, 10)

    //kills the Hero. If the killing player is a HeroPlayer,
    //the corresponding Hero is the killer
    //else the Hero is killed wihtout a killer.
    function killHero(CPlayer killer)
        if(killer instanceof HeroPlayer)
            this.killHero(heroes[(killer castTo HeroPlayer).getId()].hero)
        else
            this.killHero()

    function toString() returns string
        return "Hero "+ this.owner.getPlayer().getName() +"="+this.hero.getName()
        