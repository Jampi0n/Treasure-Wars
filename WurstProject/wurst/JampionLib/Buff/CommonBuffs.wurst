package CommonBuffs

import Buff
import ObjectIdGenerator
import AbilityObjEditing
import BuffObjEditing
import ObjectIds
import InstantDummyCaster
import MapBounds

constant MAP_HACK = compiletime(UNIT_ID_GEN.next())
constant MAP_HACK_ABILITY = compiletime(ABIL_ID_GEN.next())

constant ABILITY_STUNNED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_SILENCED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_CYCLONED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_ENSNARED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_ENTANGLED = compiletime(ABIL_ID_GEN.next())
constant ABILITY_MISS = compiletime(ABIL_ID_GEN.next())
constant ABILITY_INVIS = compiletime(ABIL_ID_GEN.next())

constant ABILITY_DISARMED_EFFECT = compiletime(ABIL_ID_GEN.next())
constant ABILITY_DISARMED_BUFF = compiletime(ABIL_ID_GEN.next())


constant BUFF_SILENCED = compiletime(BUFF_ID_GEN.next())
constant BUFF_CYCLONED = compiletime(BUFF_ID_GEN.next())
constant BUFF_ENSNARED = compiletime(BUFF_ID_GEN.next())
constant BUFF_ENSNARED_AIR = compiletime(BUFF_ID_GEN.next())
constant BUFF_ENTANGLED = compiletime(BUFF_ID_GEN.next())
constant BUFF_MISS = compiletime(BUFF_ID_GEN.next())
constant BUFF_DISARMED = compiletime(BUFF_ID_GEN.next())

public constant STUNNED = new BuffType(ABILITY_STUNNED, "creepthunderbolt", 'BPSE', false, DispelLevel.DISPEL)
public constant SILENCED = new BuffType(ABILITY_SILENCED, "soulburn", BUFF_SILENCED, false, DispelLevel.DISPEL)
public constant CYCLONED = new BuffType(ABILITY_CYCLONED, "cyclone", BUFF_CYCLONED, false, DispelLevel.DISPEL)
public constant ENSNARED = new BuffType(false, DispelLevel.DISPEL)
public constant ENTANGLED = new BuffType(ABILITY_ENTANGLED, "entanglingroots", BUFF_ENTANGLED, false, DispelLevel.DISPEL)
public constant MISS = new BuffType(ABILITY_MISS, "drunkenhaze", BUFF_MISS, false, DispelLevel.DISPEL)
public constant INVIS = new BuffType(ABILITY_INVIS, "invisibility", 'Binv', true, DispelLevel.DISPEL)

public constant DISARMED = new BuffType(false, DispelLevel.DISPEL)


public function AbilityDefinition.setDummySkill()
    this
        ..setLevels(1)
        ..setCooldown(1, 0)
        ..setCastRange(1, 92083)
        ..setManaCost(1, 0)
        ..setMissileArt("")
        ..setMissileArc(0)
        ..setMissileSpeed(10000)
        ..setTargetsAllowed(1, "air,enemies,friend,ground,invulnerable,item,neutral,structure,vulnerable,ward")
        ..setRequirements("")
        ..setRequiredLevel(6)
        ..setHeroAbility(true)
        ..setDurationHero(1, 0)
        ..setDurationNormal(1, 0)

@compiletime
function createObjectData()
    new AbilityDefinitionThunderBoltCreep(ABILITY_STUNNED)
        ..setDamage(1, 0)
        ..setDummySkill()
    new AbilityDefinitionFirelordSoulBurn(ABILITY_SILENCED)
        ..setDummySkill()
        ..setBuffs(1, BUFF_SILENCED.toRawCode())
        ..setDamageAmount(1, 0)
        ..setDamagePenalty(1, 0)
        ..setDamagePeriod(1, 300)
    new AbilityDefinitionCyclone(ABILITY_CYCLONED)
        ..setDummySkill()
        ..setBuffs(1, "Bcyc,"+BUFF_CYCLONED.toRawCode())
    new AbilityDefinitionEnsnare(ABILITY_ENSNARED)
        ..setDummySkill()
        ..setBuffs(1, BUFF_ENSNARED.toRawCode()+","+BUFF_ENSNARED_AIR.toRawCode())
    new AbilityDefinitionEntanglingRootscreep(ABILITY_ENTANGLED)
        ..setDummySkill()
        ..setDamageperSecond(1, 0)
        ..setBuffs(1, BUFF_ENTANGLED.toRawCode())
    new AbilityDefinitionInvisibility(ABILITY_INVIS)
        ..setDummySkill()
        ..setArtTarget("")
    new AbilityDefinitionBrewmasterDrunkenHaze(ABILITY_MISS)
        ..setDummySkill()
        ..setChanceToMiss(1, 1)
        ..setMovementSpeedModifier(1, 0)
        ..setAreaofEffect(1, 0.)
        ..setBuffs(1, BUFF_MISS.toRawCode())
    new AbilityDefinitionAuraSlow(ABILITY_DISARMED_BUFF)
        ..setAreaofEffect(1, 1)
        ..setMovementSpeedFactor(1, 0)
        ..setTargetsAllowed(1, "invulnerable,self,vulnerable")
        ..setBuffs(1, BUFF_DISARMED.toRawCode())
    new AbilityDefinitionCargoHoldBurrow(ABILITY_DISARMED_EFFECT)
        ..setCargoCapacity(1, 0)
    new BuffDefinition(BUFF_CYCLONED, 'Bcy2')
    new BuffDefinition(BUFF_DISARMED, 'Basl')
        ..setIcon("ReplaceableTextures\\CommandButtons\\BTNHoldPosition.blp")
        ..setArtTarget(1, "")
        ..setTooltipNormal(1, "Disarmed")
        ..setName(1, "Disarmed")
        ..setTooltipNormalExtended(1, "This unit is disarmed; it cannot attack.")
    new BuffDefinition(BUFF_ENSNARED, 'Beng')
        ..setArtTarget(1, "")
    new BuffDefinition(BUFF_ENSNARED_AIR, 'Bena')
        ..setArtTarget(1, "")
    new BuffDefinition(BUFF_ENTANGLED, 'BEer')
        ..setArtTarget(1, "")
    new BuffDefinition(BUFF_MISS, 'Bcrs')
        ..setArtTarget(1, "")
        ..setName(1, "Miss")
        ..setTooltipNormal(1, "Miss")
        ..setTooltipNormalExtended(1, "This unit misses its attacks.")
    new BuffDefinition(BUFF_SILENCED, 'BNsi')
        ..setArtTarget(1, "")
        ..setName(1, "Silence")

    new AbilityDefinitionDetectSentryWard(MAP_HACK_ABILITY)
        ..setCastRange(1, 99999)

    new UnitDefinition(MAP_HACK, 'oeye')
        ..setNormalAbilities("Aloc,Aeth,"+MAP_HACK_ABILITY.toRawCode())
        ..setModelFile("")
        ..setShadowImageUnit("")
        ..setCollisionSize(0)
        ..setSightRadiusDay(0)
        ..setSightRadiusNight(0)

        

init

    CreateFogModifierRect(DUMMY_PLAYER, FOG_OF_WAR_VISIBLE, boundRect, false, true)
        ..start()
    createUnit(DUMMY_PLAYER, MAP_HACK, playableCenter, angle(0))

    DISARMED.setupBuffWithIcon(BUFF_DISARMED, ABILITY_DISARMED_BUFF, ABILITY_DISARMED_EFFECT)

    ENSNARED.addSetupFunction() (Buff thisBuff, int factor) ->
        let target = thisBuff.getTarget()
        if factor == 1
            InstantDummyCaster.castTarget(DUMMY_PLAYER, ABILITY_ENSNARED, 1, "ensnare", target, target.getPos())
        else
            target.removeAbility(BUFF_ENSNARED)
            target.removeAbility(BUFF_ENSNARED_AIR)   
