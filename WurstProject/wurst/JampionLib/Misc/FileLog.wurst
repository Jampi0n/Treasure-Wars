/**
    This package implements a text file based log.
    The ingame log is limited in its size and cannot be searched.

    The default FileLog FILE_LOG can be used without having to create a new one.
    It uses autoSave, which means the log is rewritten whenever something is logged.
    This is very inefficient, if the log becomes large. This is mainly useful for debugging.
**/
package FileLog

import NoWurst
import LinkedList
import TypeCasting

public constant FILE_LOG = new FileLog("FileLog.txt", true)

public class FileLog
    private bool autoSave 
    private string fileName
    private LinkedList<string> lines

    construct(string fileName, bool autoSave)
        this.fileName = fileName
        this.autoSave = autoSave
        this.lines = new LinkedList<string>
        writePreload()

    function writeLine(string line)
        lines.add(line)
        if autoSave
            writePreload()

    function write(string text)
        lines.add(lines.pop() + text)
        if autoSave
            writePreload()

    function clear()
        lines.clear()
        if autoSave
            writePreload()

    function writePreload()
        PreloadGenClear()
        PreloadGenStart()
        for line in this.lines
            Preload(line)

        PreloadGenEnd(fileName)

init
    realToIndex(0)
